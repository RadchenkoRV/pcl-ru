\chapter{Переменные}
\label{ch:06}

Следующий базовый строительный блок, с которым нам нужно ознакомиться, ---
переменные. Common Lisp поддерживает два вида переменных: лексические и
динамические\footnote{Динамические переменные также иногда называют \textit{специальными
    переменными} по причинам, рассматриваемым далее в этой главе. Очень важно чтобы вы
  знали об этом синониме, так как некоторые люди (и реализации Lisp) использует один
  термин, а другие --- другой.}. Эти два типа переменных примерно соответствуют
"локальным" и "глобальным" переменным других языков. Однако, это соответствие лишь
приблизительное. С одной стороны "локальные" переменные некоторых языков в
действительности гораздо ближе к динамическим переменным Common Lisp \footnote{Ранние
  реализации Lisp использовали динамические переменные в качестве локальных, по крайней
  мере при интерпретации. Elisp, диалект Lisp, используемый в Emacs, является в некоторой
  мере устаревшим в этом отношении, продолжая поддерживать только динамические
  переменные. Некоторые другие языки повторили этот путь от динамических к лексическим
  переменным: например, локальные переменные в Perl являются динамическим, в то время как
  my-переменные, введенные в Perl 5 --- лексические. Python никогда не имел настоящих
  динамических переменных, но ввел настоящую лексическую область видимости только с версии
  2.2. (Лексические переменные Python все еще ограничены в сравнении с Lisp из-за
  объединения присваивания (assignment) и связывания (binding) в синтаксисе языка.) }. И,
с другой, локальные переменные некоторых других языков имеют \textit{лексическую область
  видимости} не предоставляя всех возможностей, предоставляемых лексическими переменными
Common Lisp. В частности, не все языки, предоставляющие переменные, имеющие лексическую
область видимости, поддерживают замыкания.

Чтобы сделать все еще более запутанным, многие формы, которые работают с переменными,
могут использоваться как с лексическими, так и с динамическими переменными. Поэтому я
начну с обсуждения некоторых аспектов переменных Lisp, которые применимы к обоим видам
переменных, а затем рассмотрю специфические характеристики лексических и динамических
переменных. Далее я обсужу оператор присваивания общего назначения Common Lisp ---
\textbf{SETF}, который используется для присваивания новых значений переменным, а также --
почти любым местам, которые могут хранить значения.

\section{Основы переменных}

Как и в других языках, в Common Lisp переменные являются именованными местами, которые
могут содержать значения. Однако, в Common Lisp переменные не типизированы таким же
образом, как в таких языках, как Java или C++. То есть вам не нужно описывать тип
объектов, которые может содержать каждая переменная. Вместо этого, переменная может
содержать значения любого типа и сами значения содержат информацию о типе, которая может
быть использована для проверки типов во время выполнения. Таким образом, Common Lisp
является \textit{динамически типизированным}: ошибки типов выявляются
динамически. Например, если вы передадите не число в функцию +, Common Lisp сообщит вам об
ошибке типов. С другой стороны, Common Lisp является \textit{строго типизированным} языком
в том смысле, что все ошибки типов будут обнаружены: нет способа представить объект в
качестве экземпляра класса, которым он не является\footnote{В действительности не совсем
  корректно говорить о том, что все ошибки типов всегда будут обнаружены: существует
  возможность использования опциональных деклараций для указания компилятору того, что
  определенные переменные всегда будут содержать объекты определенного типа, и для
  отключения проверок типов во время выполнения определенных участков кода.}.

Все значения в Common Lisp, по крайней мере концептуально, являются ссылками на объекты\footnote{В целях оптимизации определенные виды объектов, такие как целые числа, меньшие определенного размера, и знаки могут быть представлены непосредственно в памяти, другие же объекты будут представляться указателями на действительные объекты. Однако, поскольку целые числа и знаки являются неизменяемыми значениями, не имеет значения тот факт, что может существовать несколько копий "одного и того же" объекта в различных переменных. Это и является корнем различия между \textbf{EQ} и \textbf{EQL}, описанного в главе 4.}. Поэтому присваивание переменной нового значения изменяет то, на \textit{какой} объект ссылается переменная (то есть, куда ссылается переменная), но не оказывает никакого влияния на объект, на который переменная ссылалась ранее. Однако, если переменная содержит ссылку на изменяемый объект, вы можете использовать данную ссылку для изменения этого объекта, и это изменение будет видимо любому коду, который имеет ссылку на этот же объект.

Один из способов введения новой переменной вы уже использовали при определении параметров
функции. Как вы видели в предыдущей главе, при определении функции с помощью
\textbf{DEFUN} список параметров определяет переменные, которые будут содержать аргументы,
переданные функции при вызове. Например, следующая функция определяет три переменные для
хранения своих аргументов: x, y и z.

\begin{lstlisting}
  (defun foo (x y z) (+ x y z))
\end{lstlisting}

При каждом вызове функции, Lisp создает новые \textit{привязки} (\textit{bindings}) для
хранения аргументов, переданных при вызове этой функции. Привязка является проявлением
переменной во время выполнения. Отдельная переменная --- сущность, на которую вы можете
сослаться в исходном коде своей программы --- может иметь множество различных привязок за
время выполнения программы. Отдельная переменная даже может иметь множество привязок в
одно и то же время: параметры рекурсивной функции, например, связываются заново (rebound)
при каждом вызове этой функции.

Другой формой, позволяющей вводить новые переменные, является специальный оператор
\textbf{LET}. Шаблон формы \textbf{LET} имеет следующий вид:

\begin{lstlisting}
  (let (variable*) body-form*)
\end{lstlisting}

где каждая \textit{variable} является формой инициализации переменной. Каждая форма
инициализации является либо списком, содержащим имя переменной и форму начального
значения, либо, как сокращение для инициализации переменной в значение \textbf{NIL},
просто именем переменной. Следующая форма \textbf{LET}, например, связывает три переменные
x, y и z с начальными значениями 10, 20 и \textbf{NIL}:

\begin{lstlisting}
  (let ((x 10) (y 20) z)
    ...)
\end{lstlisting}

При вычислении формы \textbf{LET} сначала вычисляются все формы начальных значений. Затем,
перед выполнением форм тела, создаются и инициализируются в соответствующие начальные
значения новые привязки. Внутри тела \textbf{LET} имена переменных ссылаются на только что
вновь созданные привязки. После \textbf{LET} имена продолжают ссылаются на то, на что они
ссылались перед \textbf{LET} (если они на что-то ссылались).

Значение последнего выражения тела возвращается как значение выражения \textbf{LET}. Как и
параметры функций, переменные, вводимые \textbf{LET}, связываются заново (rebound) каждый
раз, когда поток управления заходит в \textbf{LET}\footnote{Переменные в формах \textbf{LET} и
параметры функций создаются с помощью одного и того же механизма. На самом деле в
некоторых диалектах Lisp (но не в Common Lisp) \textbf{LET} является просто макросом,
который расширяется (expands) в вызов анонимной функции. Таким образом, в таких диалектах
следующее:

\begin{lstlisting}
  (let ((x 10)) (format t "~a" x))
\end{lstlisting}


является формой макроса, которая расширяется в следующее:

\begin{lstlisting}
  ((lambda (x) (format t "~a" x) 10)
\end{lstlisting}
}.

\textit{Область видимости} (\textit{scope}) параметров функций и переменных \textbf{LET}
--- область программы, где имя переменной может быть использовано для ссылки на привязку
переменной --- ограничивается формой, которая вводит переменную. Такая форма (определение
функции или \textbf{LET}) называется \textit{связывающей формой} (\textit{binding
  form}). Как вы скоро увидите, два типа переменных (лексические и динамические)
используют два несколько отличающихся механизма области видимости, но в обоих случаях
область видимости ограничена связывающей формой.

Если вы записываете вложенные связывающие формы, которые вводят переменные с одинаковыми
именами, то привязки внутренних переменных скрывают внешние привязки. Например, при вызове
следующей функции для параметра x создается привязка для хранения аргумента функции. Затем
первая \textbf{LET} создает новую привязку с начальным значением 2, а внутренняя
\textbf{LET} создает еще одну привязку с начальным значением 3. Комментарии справа
указывают область видимости каждой привязки.

\begin{lstlisting}
  (defun foo (x)
    (format t "Параметр: ~a~%" x)            ; |<------ x - аргумент
    (let ((x 2))                             ; |
      (format t "Внешний LET: ~a~%" x)       ; | |<---- x = 2
      (let ((x 3))                           ; | |
        (format t "Внутренний LET: ~a~%" x)) ; | | |<-- x = 3
      (format t "Внешний LET: ~a~%" x))      ; | |
    (format t "Параметр: ~a~%" x))           ; |
\end{lstlisting}

Каждое обращение к x будет ссылаться на привязку с наименьшей окружающей областью
видимости. Как только поток управления покидает область видимости какой-то связывающей
формы, привязка из непосредственно окружающей области видимости перестает скрываться и x
ссылается уже на нее. Таким образом, результатом вызова foo будет следующий вывод:

\begin{verbatim}
  CL-USER> (foo 1)
  Параметр: 1
  Внешний LET: 2
  Внутренний LET: 3
  Внешний LET: 2
  Параметр: 1
  NIL
\end{verbatim}

В последующих главах я обсужу другие конструкции, которые также служат связывающими
формами (вообще, любая конструкция, вводящая новые имена переменных, могущие
использоваться только внутри этой конструкции, является связывающей формой).

Например, в главе\ref{ch:07} вы встретите цикл \textbf{DOTIMES}, простой цикл-счетчик. Он
вводит переменную, которая содержит значение счетчика, увеличивающегося на каждой итерации
цикла. Например, следующий цикл, печатающий числа от 0 до 9, связывает переменную x:

\begin{lstlisting}
  (dotimes (x 10) (format t "~d " x))
\end{lstlisting}

Ещё одной связывающей формой является вариант \code{LET}: \textbf{\code{LET*}}. Различие
состоит в том, что в \textbf{LET} имена переменных могут быть использованы только в теле
\textbf{LET} (части \textbf{LET}, идущей после списка переменных), а в \textbf{LET* }формы
начальных значений для каждой переменной могут ссылаться на переменные, введенные ранее в
списке переменных. Таким образом, вы можете записать следующее:

\begin{lstlisting}
  (let* ((x 10)
         (y (+ x 10)))
    (list x y))
\end{lstlisting}

но не так:

\begin{lstlisting}
  (let ((x 10)
        (y (+ x 10)))
    (list x y))
\end{lstlisting}

Однако, вы можете добиться такого же результата при помощи вложенных \textbf{LET}.

\begin{lstlisting}
  (let ((x 10))
    (let ((y (+ x 10)))
      (list x y)))
\end{lstlisting}

\section{Лексические переменные и замыкания}

По умолчанию все связывающие формы в Common Lisp вводят переменные \textit{лексической
  области видимости} (\textit{lexically scoped}). На переменные лексической области
видимости можно ссылаться только в коде, который текстуально находится внутри связывающей
формы. Лексическая область видимости должна быть знакома каждому, кто программировал на
Java, C, Perl или Python, так как все они предоставляют "локальные" переменные, имеющие
лексическую область видимости. Программисты на Algol также должны чувствовать себя хорошо,
так как этот язык первым ввел лексическую область видимости в 1960-х.

Однако, лексические переменные Common Lisp несколько искажают понятие лексической
переменной, по крайней мере в сравнении с оригинальной моделью Algol. Это искажение
проявляется при комбинировании лексической области видимости со вложенными функциями. По
правилам лексической области видимости, только код, текстуально находящийся внутри
связывающей формы, может ссылаться на лексическую переменную. Но что произойдет, когда
анонимная функция содержит ссылку на лексическую переменную из окружающей области
видимости? Например, в следующем выражении:

\begin{lstlisting}
  (let ((count 0)) #'(lambda () (setf count (+ 1 count))))
\end{lstlisting}

ссылка на count внутри формы \textbf{LAMBDA} допустима в соответствии с правилами
лексической области видимости. Однако, анонимная функция, содержащая ссылку, будет
возвращена как значение формы \textbf{LET}, и она может быть вызвана с помощью
\textbf{FUNCALL} кодом, который \textit{не} находится в области видимости
\textbf{LET}. Так что же произойдет? Как выясняется, если count является лексической
переменной, все работает. Привязка count, созданная когда поток управления зашел в форму
\textbf{LET}, остается столько, сколько это необходимо, в данном случае до тех пор, пока
что-то сохраняет ссылку на функциональный объект, возвращенный формой
\textbf{LET}. Анонимная функция называется \textit{замыканием} (\textit{closure}), потому
что она "замыкается вокруг" привязки, созданной \textbf{LET}.

Ключевым моментом для понимания замыканий является то, что захватывается не значение
переменной, а привязка. Поэтому замыкание может не только иметь доступ к значению
переменной, вокруг которой оно "замкнуто", но и присваивать ей новые значения, которые
будут сохраняться между вызовами замыкания. Например, вы можете захватить замыкание,
созданное предыдущим выражением, в глобальную переменную следующим образом:

\begin{lstlisting}
  (defparameter *fn* (let ((count 0)) #'(lambda () (setf count (1+ count)))))
\end{lstlisting}

Затем, при каждом его вызове значение счетчика будет увеличиваться на единицу.

\begin{verbatim}
  CL-USER> (funcall *fn*)
  1
  CL-USER> (funcall *fn*)
  2
  CL-USER> (funcall *fn*)
  3
\end{verbatim}

Отдельное замыкание может "замыкаться вокруг" нескольких привязок переменных просто
ссылаясь на них. Также множество замыканий могут захватывать одну и ту же
привязку. Например, следующее выражение возвращает список трех замыканий, первое из
которых увеличивает значение привязки count, вокруг которой оно "замкнуто", второе --
уменьшает его, а третье -- возвращает текущее значение:

\begin{lstlisting}
  (let ((count 0))
    (list
     #'(lambda () (incf count))
     #'(lambda () (decf count))
     #'(lambda () count)))
\end{lstlisting}

\section{Динамические (специальные) переменные}

Привязки с лексической областью видимости помогают поддерживать код понятным, путём
ограничения области видимости, в которой, буквально говоря, данное имя имеет смысл. Вот
почему большинство современных языков программирования используют лексическую область
видимости для локальных переменных. Однако, иногда вам действительно может понадобиться
глобальная переменная -- переменная, к который вы можете обратиться из любой части своей
программы. Хотя неразборчивое использование глобальных переменных может привести к
"спагетти-коду" также быстро как и неумеренное использование goto, глобальные переменные
имеют разумное использование и существуют в том или ином виде почти в каждом языке
программирования\footnote{Java маскирует глобальные переменные как публичные статические
  поля, C использует внешние (extern) переменные, а переменные уровня модулей в Python и
  переменные уровня пакетов в Perl аналогично могут быть доступны отовсюду.}. И, как вы
сейчас увидите, глобальные переменные Lisp -- динамические переменные -- одновременно и
более удобны, и более гибки.

Common Lisp предоставляет два способа создания глобальных переменных: \textbf{DEFVAR} и
\textbf{DEFPARAMETER}. Обе формы принимают имя переменной, начальное значение и
опциональную строку документации. После создания переменной с помощью \textbf{DEFVAR} или
\textbf{DEFPARAMETER} имя может быть использовано где угодно для ссылки на текущую
привязку этой глобальной переменной. Как вы заметили в предыдущих главах, по соглашению,
глобальные переменные именуются именами, начинающимися и заканчивающимися *.  Далее в этой
главе вы увидите, почему очень важно следовать этому соглашению по именованию. Примеры
\textbf{DEFVAR} и \textbf{DEFPARAMETER} выглядят следующим образом:

\begin{lstlisting}
  (defvar *count* 0
    "Число уже созданных виджетов.")

  (defparameter *gap-tolerance* 0.001
    "Допустимое отклонение интервала между виджетами.")
\end{lstlisting}

Различие между этими двумя формами состоит в том, что \textbf{DEFPARAMETER} всегда
присваивает начальное значение названной переменной, а \textbf{DEFVAR} делает это только
если переменная не определена. Форма \textbf{DEFVAR} также может использоваться без
начального значения для определения глобальной переменной без установки ее значения. Такая
переменная называется \textit{несвязанной} (\textit{unbound}).

На деле, вам следует использовать \textbf{DEFVAR} для определения переменных, которые
будут содержать данные, которые вы хотите сохранять даже при изменениях исходного кода,
использующего эту переменную. Например, представьте, что две переменные, определенные
ранее, являются частью приложения управления "фабрикой виджетов"\footnote{\textit{фабрика}
  --- стандартный шаблон проектирования \textit{---прим. переводчика}}. Правильным будет
определить переменную *count* с помощью \textbf{DEFVAR}, так как число уже созданных
виджетов не становится недействительным лишь потому, что мы сделали некоторые изменения в
коде создания виджетов\footnote{Если вам специально нужно сбросить переменную, созданную с
  помощью \textbf{DEFVAR}, вы можете установить ее напрямую с помощью \textbf{SETF} или
  сделать ее несвязанной с помощью \textbf{MAKUNBOUND} и перевычислить форму
  \textbf{DEFVAR}.}.

С другой стороны, переменная *gap-tolerance* вероятно влияет некоторым образом на
поведение самого кода создания виджетов. Если вы решите, что вам нужно меньшее или большее
допустимое отклонение и, следовательно, измените значение в форме DEFPARAMETER, вы
захотите, чтобы изменение вступило в силу при перекомпиляции и перезагрузке файла.

После определения переменной с помощью \textbf{DEFVAR} или \textbf{DEFPARAMETER} вы можете
ссылаться на нее откуда угодно. Например, вы можете определить следующую функцию для
увеличения числа созданных виджетов:

\begin{lstlisting}
  (defun increment-widget-count () (incf *count*))
\end{lstlisting}

Преимуществом глобальных переменных является то, что вам не нужно передавать их в
функции. Большинство языков программирования хранят потоки стандартного ввода и вывода в
глобальных переменных именно по этой причине: вы никогда не знаете, когда именно вы
захотите напечатать что-либо на стандартный вывод, и вы не хотите, чтобы каждая функция
принимала и передавала далее аргументы, содержащие эти потоки, только потому, что
нижележащему коду они могут понадобиться.

Однако, поскольку значение, такое как поток стандартного вывода, хранится в глобальной переменной и вы написали код, ссылающийся на эту глобальную переменную, порой является заманчивым попытаться временно изменить поведение этого кода путем изменения значения переменной.

Например, представьте, что вы работаете над программой, содержащей некоторые
низкоуровневые функции журналирования, печатающие в поток, хранящийся в глобальной
переменной *standard-output*. Теперь представьте, что в какой-то части программы вы хотите
перенаправить весь вывод, генерируемый этими функциями в файл. Вы можете открыть файл и
присвоить полученный поток переменной *standard-output*. После этого вышеупомянутые
функции будут слать свой вывод в этот файл.

Это работает замечательно, пока вы не забудете восстановить исходное значение
*standard-output* после завершения действий. Если вы забудете восстановить
*standard-output*, весь остальной код программы, использующий *standard-output*, также
будет слать свой вывод в файл\footnote{Метод временного переназначения *standard-output*
  также плох если система многонитевая (multithreaded): если несколько нитей (threads)
  управления попытаются напечатать в различные потоки (streams) вывода в одно и то же
  время, они все попытаются назначить глобальной переменной поток, который они хотят
  использовать, чем помешают друг другу. Вы можете использовать блокировку для контроля
  доступа к глобальной переменной, но тогда вы не получите пользы от распараллеливания,
  так как какая-нибудь нить при печати блокирует остальные нити до завершения этой
  операции даже если эти нити хотят печатать в другой поток.}.

Но похоже, что то, что вам действительно нужно, это способ обернуть часть кода во что-то
говорящее: "Весь нижележащий код (все функции, которые он вызывает, все функции, которые
вызывают эти функции, и так далее до функций самого низкого уровня) должны использовать
\textit{это} значение для глобальной переменной *standard-output*". А затем, по завершении
работы функции верхнего уровня, старое значение *standard-output* должно быть
автоматически восстановлено.

Оказывается, что это именно те возможности, что предоставляет вам другой вид переменных
Common Lisp: динамические переменные. Когда вы связываете динамическую переменную,
например с \textbf{LET}-переменной или с параметром функции, привязка, создаваемая во
время входа в связывающую форму, заменяет глобальную привязку на все время выполнения
связывающей формы. В отличие от лексических привязок, к которым можно обращаться только из
кода, находящегося в лексической области видимости связывающей формы, к динамическим
привязкам можно обращаться из любого кода, вызываемого во время выполнения связывающей
формы\footnote{Технический термин, обозначающий интервал, в течение которого к привязке
  можно обратиться, --- \textit{протяженность} (\textit{extent}) привязки. Таким образом,
  область видимости и протяженность являются взаимодополняющими понятиями: область
  видимости относится к пространству, а протяженность --- ко времени. Лексические
  переменные имеют лексическую область видимости и неопределенную протяженность, имея
  ввиду то, что они существуют неопределенное время, определяемое тем, как долго они нужны
  кому-либо. Динамические переменные, наоборот, имеют неопределенную область видимости,
  так как они доступны отовсюду, но динамическую протяженность. Для того, чтобы запутать
  вас еще больше, стоит упомянуть, что комбинацию неопределенной области видимости и
  динамической протяженности часто неверно называют \textit{динамической областью
    видимости}.}. И оказывается, что все глобальные переменные на самом деле являются
динамическими.

Таким образом, если вы хотите временно переопределить *standard-output*, это можно сделать
просто пересвязав ее, например, с помощью \textbf{LET}.

\begin{lstlisting}
  (let ((*standard-output* *some-other-stream*))
    (stuff))
\end{lstlisting}

В любом коде, который выполняется в результате вызова stuff, ссылки на *standard-output*
будут использовать привязку, установленную с помощью \textbf{LET}. А после того как stuff
завершится и поток управления покинет \textbf{LET}, новая привязка *standard-output*
исчезнет и последующие обращения к *standard-output* будут видеть привязку, бывшую до
\textbf{LET}. В любой момент времени самая последняя установленная привязка скрывает все
остальные. Можно представить, что каждая новая привязка данной динамической переменной
помещается в стек привязок этой переменной и ссылки на эту переменную всегда используют
последнюю установленную привязку. После выхода из связывающей формы созданные в ней
привязки убираются из стека, делая видимыми предыдующие привязки\footnote{Хотя стандарт и
  не указывает, как должна быть реализована многонитевость (multithreading) в Common Lisp,
  реализации, которые предоставляют ее, следуют практике, установленной на Lisp-машинах, и
  создают динамические привязки для каждой нити. Обращение к глобальной переменной найдет
  привязку, либо установленную последней в текущей нити, либо глобальную привяку.}.

Простой пример показывает как это работает:

\begin{lstlisting}
  (defvar *x* 10)
  (defun foo () (format t "X: ~d~%" *x*))
\end{lstlisting}

\textbf{DEFVAR} создает глобальную привязку переменной *x* со значением 10. Обращение к
*x* в foo будет искать текущую привязку динамически. Если вы вызовете foo на верхнем
уровне (from the top level), глобальная привязка, созданная \textbf{DEFVAR}, будет
единственной доступной привязкой, поэтому будет напечатано 10.

\begin{verbatim}
  CL-USER> (foo)
  X: 10
  NIL
\end{verbatim}

Но вы можете использовать \textbf{LET} для создания новой привязки, которая временно
скроет глобальную привязку, и foo напечатает другое значение.

\begin{verbatim}
  CL-USER> (let ((*x* 20)) (foo))
  X: 20
  NIL
\end{verbatim}

Теперь снова вызовем foo без \textbf{LET}, она опять будет видеть глобальную привязку.

\begin{verbatim}
  CL-USER> (foo)
  X: 10
  NIL
\end{verbatim}

Теперь определим новую функцию.

\begin{lstlisting}
  (defun bar ()
    (foo)
    (let ((*x* 20)) (foo))
    (foo))
\end{lstlisting}

Обратите внимание, что средний вызов foo находится внутри \textbf{LET}, которая связывает
*x* с новым значением 20. При вызове bar вы получите следующий результат:

\begin{verbatim}
  CL-USER> (bar)
  X: 10
  X: 20
  X: 10
  NIL
\end{verbatim}

Как вы можете заметить, первый вызов foo видит глобальную привязку со значением
10. Средний вызов видит новую привязку со значением 20. А после \textbf{LET}, foo снова
видит глобальную привязку.

Как и с лексической привязкой, присваивание нового значения влияет только на текущую
привязку. Чтобы увидеть это, вы можете переопределить foo, добавив присваивание значения
переменной *x*.

\begin{lstlisting}
  (defun foo ()
    (format t "Перед присваиванием~18tX: ~d~%" *x*)
    (setf *x* (+ 1 *x*))
    (format t "После присваивания~18tX: ~d~%" *x*))
\end{lstlisting}

Теперь foo печатает значение *x*, увеличивает его на единицу, а затем печатает его
снова. Если вы просто запустите foo, вы увидите следующее:

\begin{verbatim}
  CL-USER> (foo)
  Перед присваиванием X: 10
  После присваивания  X: 11
  NIL
\end{verbatim}

Ничего удивительного. Теперь запустим bar.

\begin{verbatim}
  CL-USER> (bar)
  Перед присваиванием X: 11
  После присваивания  X: 12
  Перед присваиванием X: 20
  После присваивания  X: 21
  Перед присваиванием X: 12
  После присваивания  X: 13
  NIL
\end{verbatim}

Обратите внимание, начальное значение *x* равно 11: предыдущий вызов foo действительно
изменил глобальное значение. Первый вызов foo из bar увеличивает глобальную привязку до
12. Средний вызов не видит глобальную привязку из-за \textbf{LET}. А затем последний вызов
снова может видеть глобальную привязку и увеличивает ее с 12 до 13.

Так как это работает? Как \textbf{LET} знает, когда связывает *x*, что подразумевается
создание динамической привязки вместо обычной лексической? Она знает, так как имя было
объявлено \textit{специальным}\footnote{Вот почему динамические переменные также иногда
  называют \textit{специальными перменными} (\textit{special variables}) }. Имя каждой
переменной, определенной с помощью \textbf{DEFVAR} и \textbf{DEFPARAMETER} автоматически
глобально объявляется специальным. Это означает, что когда бы вы не использовали это имя в
связывающей форме (в форме \textbf{LET}, или как параметр функции, или в любой другой
конструкции, которая создает новую привязку переменной, вновь создаваемая привязка будет
динамической. Вот почему *соглашение* *по* *именованию* так важно: будет не очень хорошо,
если вы используете имя, о котором вы думаете как о лексической переменной, а эта
переменная окажется глобальной специальной. С одной стороны, код, который вы вызываете,
сможет изменить значение этой связи; с другой, вы сами можете скрыть связь, установленную
кодом, находящимся выше по стеку. Если вы всегда будете именовать глобальные переменные,
используя соглашение по именованию *, вы никогда случайно не воспользуетесь динамической
связью, желая создать лексическую.

Также возможно локально объявить имя специальным. Если в связывающей форме вы объявите имя
специальным, привязка, созданная для этой переменной, будет динамической, а не
лексической. Другой код может локально определить имя специальным, чтобы обращаться к
динамической привязке. Однако, локальные специальные переменные используются относительно
редко, поэтому вам не стоит беспокоиться о них\footnote{Если вы хотите знать об этом, вы
  можете взглянуть на \textbf{DECLARE}, \textbf{SPECIAL} и \textbf{LOCALLY} в HyperSpec.}.

Динамические привязки делают глобальные переменные гораздо более гибкими, но важно понимать, что они позволяют осуществлять незаметные действия на расстоянии. Связывание глобальной переменной имеет два дальнодействующих эффекта: оно может изменить поведение нижележащего кода, а также открывает нижележащему коду возможность присваивания нового значения привязке, установленной выше по стеку. Вы должны использовать динамические переменные только в том случае, если вам нужно получить преимущества от одного или обоих из этих эффектов.

\section{Константы}

Еще одним видом переменных, вообще не упомянутых ранее, являются оксюморонические
"константные переменные". Все константы являются глобальными и определяются с помощью
\textbf{DEFCONSTANT}. Базовая форма \textbf{DEFCONSTANT} подобна \textbf{DEFPARAMETER}.

\begin{lstlisting}
  (defconstant name initial-value-form [ documentation-string ])
\end{lstlisting}

Как и в случае с \textbf{DEFPARAMETER}, \textbf{DEFCONSTANT} оказывает глобальный эффект
на используемое имя: после этого, имя может быть использовано только для обращения к
константе; оно не может быть использовано как параметр функции или быть пересвязано с
помощью любой другой связывающей формы. Поэтому многие программисты на Lisp следуют
соглашению по именованию и используют для констант имена начинающиеся и заканчивающиеся
знаком +. Этому соглашению следуют немного в меньшей степени, чем соглашению для
глобальных динамических имен, но оно является хорошей идеей по сходным
причинам\footnote{Несколько ключевых констант, определенных самим языком, не следуют этому
  соглашению: в первую очередь T и NIL. Это иногда раздражает, например, когда кто-то
  хочет использовать t как имя глобальной переменной. Другим примером является PI, которая
  содержит наилучшую апроксимацию математической константы пи в виде числа с плавающей
  точкой.}.

Ещё один важный момент: несмотря на то что язык позволяет вам переопределять константы
путем перевычисления \textbf{DEFCONSTANT} с другой формой начального значения, не
определено то, что именно произойдет после такого переопределения. На практике,
большинство реализаций требуют, чтобы вы перевычислили любой код, ссылающийся на
константу, чтобы изменение вступило в силу, так как старое значение могло быть встроено
(inlined). Следовательно, правильным будет использовать \textbf{DEFCONSTANT} для
определения только тех вещей, которые \textit{действительно} являются константами, такие
как значение NIL. Для вещей, которые вам может когда-нибудь понадобиться изменить, следует
использовать \textbf{DEFPARAMETER}.

\section{Присваивание}

После создания привязки вы можете совершать с ней два действия: получить текущее значение
и установить ей новое значение. Как вы видели в главе 4, символ вычисляется в значение
переменной, которую он именует, поэтому вы можете получить текущее значение просто
обратившись к переменной. Для присваивания нового значения привязке используйте макрос
\textbf{SETF}, являющийся в Common Lisp оператором присваивания общего назначения. Базовая
форма \textbf{SETF} следующая:

\begin{lstlisting}
  (setf place value)
\end{lstlisting}

Так как \textbf{SETF} является макросом, он может оценить форму "места", которому он
осуществляет присваивание и расшириться (expand) в соответствующие низкоуровневые
операции, осуществляющие необходимые действия. Когда "место" является переменной, этот
макрос расширяется в вызов специального оператора \textbf{SETQ}, который, как специальный
оператор, имеет доступ и к лексическим, и к динамическим привязкам\footnote{Некоторые
  программисты на Lisp старой школы предпочитают использовать \textbf{SETQ} с переменными,
  но современный стиль склоняется к использованию \textbf{SETF} для всех операций
  присваивания.}. Например, для присваивания значения 10 переменной x вы можете написать
это:

\begin{lstlisting}
  (setf x 10)
\end{lstlisting}

Как я рассказал ранее, присваивание нового значения привязке не оказывает никакого влияния
на остальные привязки этой переменной. И оно не оказывает никакого влияния на значение,
которое хранилось в привязке до присваивания. Таким образом, \textbf{SETF} в следующей
функции:

\begin{lstlisting}
  (defun foo (x) (setf x 10))
\end{lstlisting}

не окажет никакого влияния на любое значение вне foo. Привязка, которая создается при
вызове foo, устанавливается в 10, незамедлительно заменяя то значение, что было передано в
качестве аргумента. В частности, следующая форма:

\begin{lstlisting}
  (let ((y 20))
    (foo y)
    (print y))
\end{lstlisting}

напечатает 20, а не 10, так как именно оно является значением \code{y}, которое передается
\code{foo}, где уже является значением переменной \code{x} перед тем, как \textbf{SETF}
дает \code{x} новое значение.

\textbf{SETF} также может осуществить последовательное присваивание множеству
"мест". Например, вместо следующего:

\begin{lstlisting}
  (setf x 1)
  (setf y 2)
\end{lstlisting}

вы можете записать следующее:

\begin{lstlisting}
  (setf x 1 y 2)
\end{lstlisting}

\textbf{SETF} возвращает присвоенное значение, поэтому вы можете вкладывать вызовы
\textbf{SETF} как в следующем примере, который присваивает и x, и y одинаковое случайное
значение:

\begin{lstlisting}
  (setf x (setf y (random 10)))
\end{lstlisting}

\section{Обобщенное присваивание}

Привязки переменных, конечно, не являются единственными "местами", которые могут содержать
значения. Common Lisp поддерживает составные структуры данных, такие как массивы,
хэш-таблицы, списки, а также определенные пользователем структуры данных --- такие
структуры состоят из множества "мест", способных содержать значения.

Я опишу эти структуры данных в последующих главах, но так как мы рассматриваем
присваивание, вы должны знать, что \textbf{SETF} может присвоить значение любому
"месту". Когда я буду описывать различные составные структуры данных, я буду указывать,
какие функции могут использоваться как "места, обрабатываемые \textbf{SETF}"
("\textbf{SETFable} places"). Кратко же можно сказать, что если вам нужно присвоить
значение "месту", почти наверняка следует использовать \textbf{SETF}. Возможно даже
расширить \textbf{SETF} для того, чтобы он мог осуществлять присваивание определенным
пользователем "местам", хотя я не описываю такие возможности\footnote{Взгляните на
  \textbf{DEFSETF}, \textbf{DEFINE-SETF-EXPANDER} для получения дополнительной
  информации.}.

В этом отношении \textbf{SETF} не отличается от оператора присваивания~\code{=} языков,
произошедших от~C. В этих языках оператор~\code{=} присваивает новые значения переменным,
элементам массивов, полям классов. В языках, таких как Perl и Python, которые поддерживают
хэш-таблицы как встроенные типы данных, \code{=}~может также устанавливать значения
элементов хэш-таблицы. Таблица~\ref{table:06-1} резюмирует различные способы, которыми
используется~\code{=} в этих языках.

\begin{figure}[tb]
\begin{tabular}{|>{\centering}m{25mm}|>{\centering}m{25mm}|>{\centering}m{25mm}|>{\centering}m{25mm}|}
Присваивание ...         & Java, C, C++        & Perl                & Python \\
... переменной           & \lstinline!x = 10;! & \lstinline!$x = 10;! & \lstinline!x = 10! \\
... элементу массива     & \lstinline!a[0] = 10;! & \lstinline!$a[0] = 10;! & \lstinline!a[0] = 10! \\
... элементу хэш-таблицы & --                  & \lstinline!$hash{'key'} = 10;!  & \lstinline!hash['key'] = 10! \\
... полю объекта         & \lstinline!o.field = 10;! & \lstinline!$o->{'field'} = 10;! & \lstinline!o.field = 10! 
\end{tabular}
  \caption{Присваивание с помощью = в других языках программирования} 
  \label{table:06-1}
\end{figure}


\textbf{SETF} работает сходным образом: первый "аргумент" \textbf{SETF} является "местом"
для хранения значения, а второй предоставляет само значения. Как и с оператором = в этих
языках, вы используете одинаковую форму и для выражения "места", и для получения
значения\footnote{Широкая распространенность Algol-подобного синтаксиса для присваивания с
  "местом" слева от = и новым значением справа от него породило термины \textit{lvalue},
  сокращенно от "left value", что означает нечто, чему можно присваивать, и
  \textit{rvalue}, означающее нечто, предоставляющее значение. Хакеры компилятора обычно
  говорят: "\textbf{SETF} рассматривает свой первый аргумент как lvalue".}. Таким образом,
эквиваленты вышеприведенных в таблице 6-1 присваиваний для Lisp следующие (\textbf{AREF}
--- функция доступа к массиву, \textbf{GETHASH} осуществляет операцию поиска в
хэш-таблице, а field может быть функцией, которая обращается к слоту под именем field
определенного пользователем объекта):

\begin{verbatim}
  Простая переменная:    (setf x 10) 
  Массив:                (setf (aref a 0) 10)
  Хэш-таблица:           (setf (gethash 'key hash) 10)
  Слот с именем 'field': (setf (field o) 10)
\end{verbatim}

Обратите внимание, что присваиваение с помощью \textbf{SETF} "месту", которое является
частью большего объекта, имеет ту же семантику, что и присваивание переменной: "место"
модифицируется без оказания какого-либо влияния на объект, который хранился там до
этого. И вновь, это подобно тому, как ведет себя = в Java, Perl и
Python\footnote{Программисты на C могут хотеть думать о переменных и других "местах" как о
  содержащих указатель на действительный объект; присваивание переменной просто изменяет
  то, на какой объект она указывает, а присваивание значения части составного объекта
  подобно косвенному обращению по указателю к действительному объекту. Программисты на C++
  должны обратить внимание, что поведение оператора = в C++ при обращении с объектами (а
  именно memberwise FIXME копирование) абсолютно отличается.}.

\section{Другие способы изменения <<мест>>}

В то время как все присваивания можно выразить с помощью \textbf{SETF}, некоторые образцы,
включающие присваивания нового значения, основанного на текущем значении, являются
достаточно общими для того, чтобы получить свои собственные операторы. Например, вы можете
увеличить число с помощью \textbf{SETF} следующим образом:

\begin{lstlisting}
  (setf x (+ x 1))
\end{lstlisting}

или уменьшить его так:

\begin{lstlisting}
  (setf x (- x 1))
\end{lstlisting}

Но это слегка утомительно по сравнению с стилем C: \code{++x} и \code{--x}. Вместо этого
вы можете использовать макросы \textbf{INCF} и \textbf{DECF}, которые увеличивают и
уменьшают "место" на определенную величину, по умолчанию 1.

\begin{lstlisting}
  (incf x)    === (setf x (+ x 1))
  (decf x)    === (setf x (- x 1))
  (incf x 10) === (setf x (+ x 10))
\end{lstlisting}

\textbf{INCF} и \textbf{DECF} являются примерами определенного вида макросов, называемых
\textit{модифицирующими макросами} (\textit{modify macros}). Модифицирующие макросы
являются макросами, построенными поверх \textbf{SETF}, которые модифицируют "места" путем
присваивания нового значения, основанного на их текущем значении. Главным преимуществом
таких макросов является то, что они более краткие, чем аналогичные операции, записанные с
помощью \textbf{SETF}. Вдобавок, модифицирующие макросы определены таким образом, чтобы
быть безопасными при использовании с "местами", когда выражение "места" должно быть
вычислено лишь единожды. Несколько надуманным примером является следующее выражение,
которое увеличивает значение произвольного элемента массива:

\begin{lstlisting}
  (incf (aref *array* (random (length *array*))))
\end{lstlisting}

Наивный перевод этого примера в выражение, использующее \textbf{SETF}, может выглядить
следующим образом:

\begin{lstlisting}
  (setf (aref *array* (random (length *array*)))
        (1+ (aref *array* (random (length *array*)))))
\end{lstlisting}

Однако, это не работает, так как два последовательных вызова \textbf{RANDOM} не
обязательно вернут одинаковое значение: это выражение вероятно получит значение одного
элемента массива, увеличит его, а затем сохранит его как новое значение другого элемента
массива. Однако, выражение \textbf{INCF} сделает все правильно, так как знает, как
правильно разобрать это выражение:

\begin{lstlisting}
  (aref *array* (random (length *array*)))
\end{lstlisting}

чтобы извлечь те части, которые возможно могут иметь побочные эффекты, и гарантировать,
что они будут вычисляться лишь один раз. В этом случае, выражение \textbf{INCF} вероятно
расширится в нечто более или менее подобное этому:

\begin{lstlisting}
  (let ((tmp (random (length *array*))))
    (setf (aref *array* tmp) (1+ (aref *array* tmp))))
\end{lstlisting}

Вообще, модифицирующие макросы гарантируют однократное вычисление слева направо своих
аргументов, а также подформ формы места (place form).

Макрос \textbf{PUSH}, который вы использовали в примере с базой данных для добавления
элементов в переменную *db*, является еще одним модифицирующим макросом. Более подробно о
его работе и работе \textbf{POP} и \textbf{PUSHNEW} будет сказано в главе 12, где я буду
говорить о том, как представляются (represented) списки в Lisp.

И наконец, два слегка эзотерических, но полезных модифицирующих макроса --
\textbf{ROTATEF} и \textbf{SHIFTF}. \textbf{ROTATEF} циклически сдвигает значение между
"местами". Например, если вы имеете две переменные, a и b, этот вызов:

\begin{lstlisting}
  (rotatef a b)
\end{lstlisting}

обменяет значения двух переменных и вернет \textbf{NIL}. Так как a и b являются
переменными и вам не нужно беспокоиться о побочных эффектах, предыдущее выражение
\textbf{ROTATEF} эквивалентно следующему:

\begin{lstlisting}
  (let ((tmp a)) (setf a b b tmp) nil)
\end{lstlisting}

С другими видами "мест" эквивалентное выражение с использованием \textbf{SETF} может быть
более сложным.

\textbf{SHIFTF} подобен \textbf{ROTATEF} за исключением того, что вместо циклического
сдвига значений, он просто сдвигает их влево: последний аргумент предоставляет значение,
которое перемещается в предпоследний аргумент и так далее. Исходное значение первого
аргумента просто возвращается. Таким образом, следующее:

\begin{lstlisting}
  (shiftf a b 10)
\end{lstlisting}

эквивалентно (и снова, так как вам не нужно беспокоиться о побочных эффектах) следующему:

\begin{lstlisting}
  (let ((tmp a)) (setf a b b 10) tmp)
\end{lstlisting}

И \textbf{ROTATEF}, и \textbf{SHIFTF} могут использоваться с любым числом аргументов, и,
как все модифицирующие макросы, гарантируют однократное их вычисление слева направо.

С базовыми знаниями функций и переменных Common Lisp в своем арсенале вы готовы перейти к
следующей особенности, которая еще больше отличает Lisp от других языков программирования:
макросы.

%%% Local Variables: 
%%% mode: latex
%%% TeX-master: "pcl-ru"
%%% TeX-open-quote: "<<"
%%% TeX-close-quote: ">>"
%%% End: 
