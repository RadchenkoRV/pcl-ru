\chapter{Практика: База данных для MP3}
'\label{ch:27}

В этой главе мы заново рассмотрим идею, впервые упомянутую в главе~\ref{ch:03} --
построение базы данных, расположенной в памяти, на основе базовых типов данных Lisp.
Сейчас нашей целью является хранение информации, которую вы извлечёте из коллекции файлов
в формате MP3 при помощи библиотеки ID3v2 из главы~\ref{ch:25}.  Вы затем будете
использовать эту базу данных в главах~\ref{ch:28} и~\ref{ch:29} как часть потокового
MP3-сервера с Web-интерфейсом.  Конечно сейчас вы уже можете использовать некоторые из
языковых конструкций, изученных со времени изучения главы~\ref{ch:03}, чтобы создать более
совершенный код.

\section{База данных}

Основной проблемой базы данных из главы~\ref{ch:03} является то, что есть только одна
таблица -- список, сохранённый в переменной \code{*db*}.  Другой проблемой является то,
что код ничего не знает о типах значений, сохранённых в разных колонках.  В главе 3 вы
просто использовали функцию общего назначения \code{EQUAL} для сравнения значений в
колонках при выборе строк из базы данных, но у вас были бы проблемы, если бы вы хотели бы
сохранить значения, которые не сравниваются с помощью\code{EQUAL}, или если бы вы хотели
сортировать строки в базе данных, поскольку не такой функции сравнения, похожей на
\code{EQUAL}.

Сейчас вы будете решать обе проблемы путём определения класса \code{table}, который будет
описывать отдельные таблицы базы данных.  Каждый экземпляр класса \code{table} будет
состоять из двух слотов -- один для хранения данных, а второй -- для хранения информации о
колонках таблицы, которую смогут использовать функции для работы с базой данных.  Класс
выглядит примерно вот так:

\begin{lstlisting}
(defclass table ()
  ((rows   :accessor rows   :initarg :rows :initform (make-rows))
   (schema :accessor schema :initarg :schema)))
\end{lstlisting}

Также как и в главе~\ref{ch:03}, вы можете представлять отдельные строки в виде списков
свойств, но сейчас время вы создадите абстракцию, которая позволит вам изменять внутренее
представление без особых трудностей. И в данной версии вы будет сохранять данные в
векторе, а не в списке свойств, поскольку некоторые операции, которые вы будете
поддерживать, например, произвольный доступ к строкам по числовому индексу, и возможность
сортировки таблицы, могут быть более эффективно реализованы с помощью векторов.

Функция \code{make-rows}, используемая для инициализации слота \code{rows} может быть
простой обёрткой для функции \code{MAKE-ARRAY}, которая создаёт пустой вектор с изменяемым
размером и указателем заполнения.

%TODO this should be inside of table/box
Пакет

Объявление пакета для разрабатываемого вами в этой главе кода будет выглядеть следующим образом:

\begin{lstlisting}
(defpackage :com.gigamonkeys.mp3-database
  (:use :common-lisp 
        :com.gigamonkeys.pathnames
        :com.gigamonkeys.macro-utilities
        :com.gigamonkeys.id3v2)
  (:export  :*default-table-size*
            :*mp3-schema*
            :*mp3s*
            :column
            :column-value
            :delete-all-rows
            :delete-rows
            :do-rows
            :extract-schema
            :in
            :insert-row
            :load-database
            :make-column
            :make-schema
            :map-rows
            :matching
            :not-nullable
            :nth-row
            :random-selection
            :schema
            :select
            :shuffle-table
            :sort-rows
            :table
            :table-size
            :with-column-values))
\end{lstlisting}

Раздел \code{:use} даёт возможность доступа к функциям и макросам, чьи имена
экспортированы из пакетов, созданных в главах~\ref{ch:15}, \ref{ch:08} и~\ref{ch:25}, а
секция \code{:export} используется для объявления функций, реализуемых данным пакетом, и
которые будут использоваться в главе~\ref{ch:29}.

%TODO end of table/box

\begin{lstlisting}
(defparameter *default-table-size* 100)

(defun make-rows (&optional (size *default-table-size*))
  (make-array size :adjustable t :fill-pointer 0))
\end{lstlisting}

Для представление схемы таблицы, вам необходимо определить ещё один класс --
\code{column}, каждый экземпляр которого будет содержать информацию об одной колонке в
таблице: её название, способ сравнения значений в колонке на равенство и порядок
расположения, значение по умолчанию, а также функцию, которая будет использоваться для
нормализации значения при вставке данных в таблицу и при запросе данных из таблицы. Слот
\code{schema} будет хранить список объектов типа \code{column}.  Определение класса будет
выглядеть примерно вот так:

\begin{lstlisting}
(defclass column ()
  ((name               
    :reader name
    :initarg :name)

   (equality-predicate
    :reader equality-predicate
    :initarg :equality-predicate)

   (comparator
    :reader comparator
    :initarg :comparator)

   (default-value
    :reader default-value
    :initarg :default-value
    :initform nil)

   (value-normalizer
    :reader value-normalizer
    :initarg :value-normalizer
    :initform #'(lambda (v column) (declare (ignore column)) v))))
\end{lstlisting}

Слоты \code{equality-predicate} и \code{comparator} объекта \code{column} хранят функции,
которые будут использоваться для сравнения значений данной колонки на равенство и порядок
расположения.  Например, для колонки, которая будет хранить строковые значения, мы можем
использовать функции \code{STRING=} в качестве значения \code{equality-predicate} и
\code{STRING<} для \code{comparator}, тогда как колонки, хранящие числа, могут
использовать функции \code{=} и \lstinline!<!.

Слоты \code{default-value} и \code{value-normalizer} используются при вставке и при
запросе данных (слот \code{value-normalizer}).  Когда вы вставляет строку в базу данных, и
для определённой колонки не указано значение, то вы можете использовать значение,
хранящееся в слоте \code{default-value} данной колонки.  Затем, значение (значение по
умолчанию или указанное пользователем) нормализуется путём передачи его и объекта,
описывающего колонку в БД, в качестве параметров функции, указанной в слоте
\code{value-normalizer}.  Вы передаёте объект типа \code{column} поскольку для функции
\code{value-normalizer} может понадобиться некоторые данные, связанные с объектом
\code{column}. (Вы увидите пример такого использования в следующем разделе).  Вы также
должны нормализовывать значения, передаваемы в запросах, до их сравнения с объектами в
базе данных.

Таким образом, \code{value-normalizer} отвечает за возврат значения, которое может быть
спокойно переданно функциям \code{equality-predicate} и \code{comparator}.  Если
\code{value-normalizer} не может найти подходящее возвращаемое значение, то она
сгенерирует ошибку.

Другой причиной для нормализации значений до их сохранения в БД является возможность
уменьшить потребление памяти и процессора.  Например, если у вас есть колонка, которая
должна хранить строковые значения, но количество значений, которые будут сохранены
является ограниченным -- например, колонка \code{genre} (жанр) в базе данных MP3-файлов,
то вы можете уменьшить потреление памяти и увеличить скорость работы, путём использования
функции \code{value-normalizer} для интернирования (FIXME intern - написать что это такое)
строк (преобразовать все вызовы \code{STRING=} к одному объекту-строке).  Так что вам
нужно будет иметь столько строковых объектов, сколько у вас имеется различающихся строк,
вне зависимости от того, сколько строк у вас в таблице, и вы тогда сможете использовать
для сравнения функцию \code{EQL}, а не \code{STRING=}, которая является более
медленной.\footnote{Общим основанием для интернирования объектов явялется то, что когда
  вам нужно сравнивать определённое значение много раз, то стоит выполнит его
  интернирование, несмотря на некоторые затраты на эту
  операцию. Функция\code{value-normalizer} запускается один раз когда вы вставляете
  значение, и как вы увидите далее, один раз в начале каждого запроса. Поскольку запрос
  может приводить к выполнению \code{equality-predicate} для каждой из строк таблицы, то
  общие затраты на интернирование значений, быстро приближаются к нулю.}

\section{Определение схемы базы данных}

Таким образом, чтобы создать экземпляр таблицы, вам необходимо создать список объектов
\code{column} objects.  Вы можете создать такой список вручную, используя функции
\code{LIST} и \code{MAKE-INSTANCE}.  Но вы скоро заметите, что вы часто создаёте множество
объектов \code{column} с одинаковыми комбинациями функций \code{comparator} и
\code{equality-predicate}. Это происходит оттого, что комбинация функций сравнения по
существу определяет тип колонки. Было бы хорошо, если бы был способ определить имена для
этих типов, что позволит вам просто указывать, что конкретная колонка является строковй,
вместо того, чтобы указывать \code{STRING<} и \code{STRING=} в качестве функций сравнения.
Одним из способов решения этой проблемы является определение обобщённой функции,
\code{make-column}, например, вот так:

\begin{lstlisting}
(defgeneric make-column (name type &optional default-value))
\end{lstlisting}

Теперь вы можете определять методы данной обобщённой функции, специализированные для типа,
с использованием \code{EQL}, которые будут возвращать объекты \code{column} со слотами,
заполненными соответствующими значениями.  Вот определения для методов, которые определяют
типы колонок с именами \code{string} и \code{number}:

\begin{lstlisting}
(defmethod make-column (name (type (eql 'string)) &optional default-value)
  (make-instance
   'column 
   :name name
   :comparator #'string< 
   :equality-predicate #'string=
   :default-value default-value
   :value-normalizer #'not-nullable))

(defmethod make-column (name (type (eql 'number)) &optional default-value)
  (make-instance 
   'column
   :name name
   :comparator #'< 
   :equality-predicate #'=
   :default-value default-value))
\end{lstlisting}

Следующая функция -- \code{not-nullable}, используется в качестве значения
\code{value-normalizer} для строковых колонок, и просто возвращает переданное значение для
всех случаев, кроме тех, когда ей передают значение \code{NIL}, когда она сигнализирует об
ошибке:

\begin{lstlisting}
(defun not-nullable (value column)
  (or value (error "Column ~a can't be null" (name column))))
\end{lstlisting}

Это важно, поскольку вызовы \code{STRING<} и \code{STRING=} будут выдавать ошибку, если им
будут передан \code{NIL}; лучше перехватить неправильные значения до того, как они будут
вставлены в таблицу, а не тогда, когда мы будем их использовать.\footnote{Как всегда, в
  книгах по программированию правильная обработка ошибок является поводом для сокращения;
  при разработке в реальных условиях, вы скорее всего определите специальный тип ошибки, и
  будете использовать его, вместо стандартного:

\begin{lstlisting}
(error 'illegal-column-value :value value :column column)
\end{lstlisting}

Затем вы можете подумать о том, где вы можете добавить код перезапуска, который позволит
вам восстановить последствия такой ошибки.  И в заключение, в почти любом приложении, вы
должны установить обработчики событий, которые позволят выбрать соответствующий код
перезапуска.}

Еще одним типом колонки, который понадобится для базы данных MP3 является
\code{interned-string}, чьи значения интернируются, как это обсуждалось выше.  Поскольку
вам нужна хэш-таблица, в которую вы будете интернировать значения, вы должны определить
подкласс \code{column} -- \code{interned-values-column}, который добавит ещё один слот,
чьим значением будет хэш-таблица, которая будет использоваться для интернирования.

Для реализации интернирования, вам потребуется указать в качестве \code{:initform} для
слота \code{value-normalizer} функцию, которая будет интернировать значение в хэш-таблицу,
которая хранится в колонке \code{interned-values}.  И поскольку, одна из самых главных
причин интенирования значений -- возможность использования \code{EQL} в качестве функции
равенства, то вы также должны добавить \lstinline!#'eql! в качестве значения \code{:initform}
для слота \code{equality-predicate}.

\begin{lstlisting}
(defclass interned-values-column (column)
  ((interned-values
    :reader interned-values
    :initform (make-hash-table :test #'equal))
   (equality-predicate :initform #'eql)
   (value-normalizer   :initform #'intern-for-column)))

(defun intern-for-column (value column)
  (let ((hash (interned-values column)))
    (or (gethash (not-nullable value column) hash)
        (setf (gethash value hash) value))))
\end{lstlisting}

Затем вы можете определить метод \code{make-column} специализированный для имени
\code{interned-string}, который будет возвращать экземпляр \code{interned-values-column}.

\begin{lstlisting}
(defmethod make-column (name (type (eql 'interned-string)) &optional default-value)
  (make-instance 
   'interned-values-column
   :name name
   :comparator #'string< 
   :default-value default-value))
\end{lstlisting}

С помощью данных методов, определённых для \code{make-column}, вы теперь можете определить
функцию, \code{make-schema}, которая создаёт список объектов типа \code{column} из списка
описаний колонок, каждое из которых содержит имя колонки, имя типа колонки, и,
необязательно, значение по умолчанию.

\begin{lstlisting}
(defun make-schema (spec)
  (mapcar #'(lambda (column-spec) (apply #'make-column column-spec)) spec))
\end{lstlisting}

Например, с помощью следующего кода вы можете определить схему для таблицы, которая будет
использоваться для хранения данных, извлечённых из файлов MP3:

\begin{lstlisting}
(defparameter *mp3-schema* 
  (make-schema 
   '((:file     string)
     (:genre    interned-string "Unknown")
     (:artist   interned-string "Unknown")
     (:album    interned-string "Unknown")
     (:song     string)
     (:track    number 0)
     (:year     number 0)
     (:id3-size number))))
\end{lstlisting}

Чтобы создать саму таблицу для хранения информации о файлах MP3, вы должны передать
\code{*mp3-schema*} в качестве аргумента \code{:schema} функции \code{MAKE-INSTANCE}.

\begin{lstlisting}
(defparameter *mp3s* (make-instance 'table :schema *mp3-schema*))
\end{lstlisting}

\section{Вставка значений}

Сейчас вы готовы к тому, чтобы определить вашу первую операцию для работы с таблицами --
\code{insert-row}, которая получает список свойств (plist) имён и значений, и таблицу, и
добавляет строку к таблице.  Большая часть работы выполняется в дополнительной функции
\code{normalize-row}, которая создаёт список свойств для всех колонок таблицы, используя
нормализованные значения и значения по умолчанию, которые получаются из слотов
\code{names-and-values}, если значение было указано, или \code{default-value} если
значение для конкретной колонки не было указано.

\begin{lstlisting}
(defun insert-row (names-and-values table)
  (vector-push-extend (normalize-row names-and-values (schema table)) (rows table)))

(defun normalize-row (names-and-values schema)
  (loop
     for column in schema
     for name  = (name column)
     for value = (or (getf names-and-values name) (default-value column))
     collect name
     collect (normalize-for-column value column)))
\end{lstlisting}

Создание дополнительной функции \code{normalize-for-column}, которая получает значение, и
объект \code{column} и возвращает нормализованное значение, оправдано тем, что вам будет
проводить нормализацию значений при запросах к таблице.

\begin{lstlisting}
(defun normalize-for-column (value column)
  (funcall (value-normalizer column) value column))
\end{lstlisting}

Теперь вы готовы к объединению кода базы данных с кодом из предыдущих глав, чтобы
построить базу данных, содержащую информацию выделенную из файлов MP3.  Вы можете
определить функцию \code{file->row}, которая будет использовать функцию \code{read-id3} из
библиотеки \code{ID3v2} для выделения тагов ID3 из файла, и превращения их в список
свойств, который будет передан функции \code{insert-row}.

\begin{lstlisting}
(defun file->row (file)
  (let ((id3 (read-id3 file)))
    (list
     :file   (namestring (truename file))
     :genre  (translated-genre id3)
     :artist (artist id3)
     :album  (album id3)
     :song   (song id3)
     :track  (parse-track (track id3))
     :year   (parse-year (year id3))
     :id3-size (size id3))))
\end{lstlisting}

Вам не нужно беспокоиться о нормализации значений, поскольку это будет сделано в
\code{insert-row}.  Однако, вы должны сконвертировать строки, возвращённые функциями
\code{track} и \code{year} в числа.  Число \code{track} (номер композиции) -- это таг ID3,
который иногда сохраняется как число в виде строки, и иногда как число, за которым следует
(через знак слеш) ещё одно число, обозначающее количество композиций в альбоме.  Поскольку
нам нужен только номер композиции, то вы должны использовать аргумент \code{:end} при
вызове функции \code{PARSE-INTEGER} для того, чтобы указать что разбор должен
осуществляться только до знака слеш, если он есть.\footnote{Если какой-то из файлов MP3
  содержит неправильные данные в записях \code{track} и \code{year}, то
  \code{PARSE-INTEGER} может сигнализировать об ошибке. Одним из способов обойти это
  поведение -- передать функции \code{PARSE-INTEGER} параметр \code{:junk-allowed} равный
  \code{T}, который заставит функцию игнорировать любой "мусор", который следует за
  числом, и вернуть \code{NIL} если чисо не было найдено в строке.  Или, если вы хотите
  попрактиковаться в использовании системы условий и перезапусков, то вы можете определить
  специальное значение \code{error} и использовать его в качестве сигнала из этих функций,
  в том случае если данные неправильно оформлены, а также установить несколько точек
  перезапуска, чтобы позволить этим функциям обработать эти ошибки.}

\begin{lstlisting}
(defun parse-track (track)
  (when track (parse-integer track :end (position #\/ track))))

(defun parse-year (year)
  (when year (parse-integer year)))
\end{lstlisting}

В заключение, вы можете собрать все эти функции вместе с \code{walk-directory} из
библиотеки переносимых имён файлов, а также функцией \code{mp3-p} из библиотеки
\code{ID3v2} чтобы определить функцию, которая загружает в базу данных MP3 информацию
извлечённую из файлов MP3, которые были найдены в определённом каталоге (и всех его
подкаталогах).

\begin{lstlisting}
(defun load-database (dir db)
  (let ((count 0))
    (walk-directory 
     dir 
     #'(lambda (file)
         (princ #\.)
         (incf count)
         (insert-row (file->row file) db))
     :test #'mp3-p)
    (format t "~&В базу данных загружено ~d файлов." count)))
\end{lstlisting}

\section{Выполнение запросов к базе данных}

После того, как загрузите данные в базу данных, вам необходимо найти способ выполнять
запросы к ней.  Для приложения работающего с файлами MP3 вам понадобятся более сложные
функции выполнения запросов чем те, которые были использованы в главе 3.  Сейчас вам нужна
не только возможность извлекать строки отвечающие определённым критериям, но также и
возможность делать выборку только определённых колонок, и возможно, сортировать строки по
определённой колонке.  В соответствии с теорией реляционных баз данных, результатом
запроса будет новая таблица, содержащая строки и колонки.

В качестве образца для функции выполнения запросов -- \code{select} , был взят оператор
\code{SELECT} из языка SQL. Эта функция принимает пять именованных параметров:
\code{:from}, \code{:columns}, \code{:where}, \code{:distinct} и \code{:order-by}.
Аргумент \code{:from} указывает объект \code{table} для которого вы хотите выполнить
запрос.  Аргумент \code{:columns} указывает то, какие колонки должны быть включены в
результат.  В качестве значения должен быть указан список имён колонок, имя одной колонки
или \code{T} (значение по умолчанию), указывающее, что должны быть включены все колонки.
Аргумент \code{:where} (если он указан), должен быть функцией, которая получает строку, и
возвращает истинное значение, если эта строка должна быть включена в результаты.  Немного
спустя, вы напишете две функции -- \code{matching} и \code{in}, которые возвращают
функции, допустимые для использования в качестве аргумента \code{:where}.  Аргумент
\code{:order-by} (если он указан), должен быть списком имён колонок; результаты будут
отсортированы по соответствующим колонкам.  Также как и для аргумента \code{:columns}, вы
можете указать лишь одну колонку, просто используя её имя, что эквивалентно списку из
одного элемента.  В заключение, аргумент \code{:distinct} является логическим значением,
которое указывает -- должны ли мы удалять дублирующиеся строки из результата.  Значением
по умолчанию для \code{:distinct} является \code{NIL}.

Вот несколько примеров использования \code{select}:

\begin{lstlisting}
;; Выбрать все строки где колонка :artist равна "Green Day"
(select :from *mp3s* :where (matching *mp3s* :artist "Green Day"))

;; Получить отсортированный список артистов, исполняющих песни в жанре "Rock"
(select
  :columns :artist
  :from *mp3s*
  :where (matching *mp3s* :genre "Rock")
  :distinct t
  :order-by :artist)
\end{lstlisting}

Реализация \code{select} вместе со вспомогательными функциями выглядит примерно так:

\begin{lstlisting}
(defun select (&key (columns t) from where distinct order-by)
  (let ((rows (rows from))
        (schema (schema from)))

    (when where
      (setf rows (restrict-rows rows where)))

    (unless (eql columns 't)
      (setf schema (extract-schema (mklist columns) schema))
      (setf rows (project-columns rows schema)))

    (when distinct
      (setf rows (distinct-rows rows schema)))

    (when order-by
      (setf rows (sorted-rows rows schema (mklist order-by))))

    (make-instance 'table :rows rows :schema schema)))

(defun mklist (thing)
  (if (listp thing) thing (list thing)))

(defun extract-schema (column-names schema)
  (loop for c in column-names collect (find-column c schema)))

(defun find-column (column-name schema)
  (or (find column-name schema :key #'name)
      (error "No column: ~a in schema: ~a" column-name schema)))

(defun restrict-rows (rows where)
  (remove-if-not where rows))

(defun project-columns (rows schema)
  (map 'vector (extractor schema) rows))

(defun distinct-rows (rows schema)
  (remove-duplicates rows :test (row-equality-tester schema)))

(defun sorted-rows (rows schema order-by)
  (sort (copy-seq rows) (row-comparator order-by schema)))
\end{lstlisting}

Конечно, самыми интересными частями \code{select} является реализация функций
\code{extractor}, \code{row-equality-tester} и \code{row-comparator}.

Как вы можете заключить из того, как эти функции используются, каждая из этих функций
должна возвращать новую функцию.  Например, функция \code{project-columns} использует
значение, возвращённое функцией \code{extractor} в качестве аргумента функции \code{MAP}.
Поскольку \code{project-columns} предназначена для возврата набора строк с только
определёнными значениями колонок, вы можете заключить, что \code{extractor} возвращает
функций, которая получает строку в качестве аргумента, и возвращает новую строку, которая
содержит только колонки, указанные в переданной ей схеме.  Вот как мы можем реализовать
эту функцию:

\begin{lstlisting}
(defun extractor (schema)
  (let ((names (mapcar #'name schema)))
    #'(lambda (row)
        (loop for c in names collect c collect (getf row c)))))
\end{lstlisting}

Отметьте то, как вы можете выполнить задачу по извлечению имён из схемы за пределами тела
замыкания -- поскольку замыкание может быть вызвано несколько раз, вы захотите чтобы в нем
выполнялось как можно меньше действий при каждом вызове.

Функции \code{row-equality-tester} и \code{row-comparator} реализуются аналогичным
образом.  Для того, чтобы принять решение о равенстве двух строк, вам необходимо применить
соответствующие функции сравнения каждой из колонок к значениям соответствующих колонок.
Из материала главы~\ref{ch:22} мы знаем, что \code{LOOP} всегда возвращает \code{NIL}
когда пара значений не проходит тест, в противном случае \code{LOOP} вернёт \code{T}.

\begin{lstlisting}
(defun row-equality-tester (schema)
  (let ((names (mapcar #'name schema))
        (tests (mapcar #'equality-predicate schema)))
    #'(lambda (a b)
        (loop for name in names and test in tests
           always (funcall test (getf a name) (getf b name))))))
\end{lstlisting}

Расположение двух строк по порядку -- более сложная задача.  В Lisp функции сравнения
возвращают истинное значение, если первый аргумент должен быть расположен перед вторым
аргументом, или \code{NIL} в противном случае.  Таким образом, \code{NIL} может означать,
что второй аргумент должен быть расположен перед первым аргументом, или оба аргумента
равны. Мы также хотим, чтобы функции сравненпия строк вели себя точно также -- возвращали
\code{T} если первая строка должна быть перед второй, и \code{NIL} в противном случае.

Таким образом, для сравнения двух строк, вы должны сравнить значения тех колонок, по
которым вы будете проводить сортировку, используя соответствующую функцию сравнения для
каждой из колонок.  Сначала вызывается функция сравнения со значением из первой строки в
качестве первого аргумента.  Если функция сравнения вернёт истинное значение, то это
означает, что первая колонка точно должна распологаться перед второй колонкой, так что вы
можете сразу вернуть значение \code{T}.

Но если функция сравнения вернула \code{NIL}, то вам нужно определить почему это произошло
-- либо второе значение должно быть поставлено перед первым, или они равны.  Так что вам
необходимо снова вернуть функцию сравнения, но поменять аргументы местами.  Если функция
сравнения вернёт истинное значение, то это означает, что вторая строка должна стоять перед
первой, и вы можете сразу вернуть \code{NIL}.  В противном случае, значения в данной
колонке равны, и вам необходимо перейти к следующей колонке.  Если вы проверите все
колонки и не получите однозначного сравнения в пользу одной из строк, то эти строки равны,
и вы должны вернуть \code{NIL}.  Функция, которая реализует такой алгоритм, будет
выглядеть следующим образом:

\begin{lstlisting}
(defun row-comparator (column-names schema)
  (let ((comparators (mapcar #'comparator (extract-schema column-names schema))))
    #'(lambda (a b)
        (loop
           for name in column-names
           for comparator in comparators
           for a-value = (getf a name)
           for b-value = (getf b name)
           when (funcall comparator a-value b-value) return t
           when (funcall comparator b-value a-value) return nil
           finally (return nil)))))
\end{lstlisting}

\section{Функции отбора (FIXME Matching)}

Аргумент \code{:where} функции \code{select} может быть любой функцией, которая принимает
в качестве аргумента строку и возвращает истинное значение, если она должна быть включена
в результаты.  Однако на практике, вам редко понадобится вся мощь вручную написанного кода
для выражения критериев запроса.  Так что вы должны лишь реализовать две функции:
\code{matching} и \code{in}, которые будут создавать функции запроса, которые позволят вам
создавать общие виды запросов, а также возьмут на себя заботу об использовании
соответствующих функций равенства и нормализации для каждой из колонок.

FIXME The workhouse \code{query-function} constructor will be функция \code{matching},
которая возвращает функцию, которая будет сравнивать строку с конкретными значениями
колонок.  Вы увидели как она может быть использована в предыдущих примерах \code{select}.
Например, такой вызов \code{matching}:

\begin{lstlisting}
(matching *mp3s* :artist "Green Day")
\end{lstlisting}

вернёт функцию, которая будет выбирать строки, в которых значение колонки \code{:artist}
равно \code{"Green Day"}.  Вы также можете передавать множество имён колонок и значений --
возвращаемая функция будет возвращать истинное значение только тогда, когда все колонки
имеют заданные значения.  Например, следующий вызов вернёт замыкание, которое будет
принимать строки, в которых артист равен \code{"Green Day"} и альбом равен \code{"American
  Idiot"}:

\begin{lstlisting}
(matching *mp3s* :artist "Green Day" :album "American Idiot")
\end{lstlisting}

Вам необходимо передать функции \code{matching} объект \code{table}, поскольку функции
необходим доступ к схеме таблицы для получения функций сравнения и нормализации для тех
колонок, для которых выполняется отбор данных.

Вы строите функцию, возвращаемую функцией \code{matching} из меньших функций, каждая из
которые отвечает за проверку значения одной из колонок.  Для того, чтобы создать эти
функции, вы должны определить функцию \code{column-matcher}, которая получает объект
\code{column} и не нормализованное значение и возвращает функцию, которая получает строку
и возвращает истинное значение в том случае, если значение заданной колонки соответствует
нормализованному значению заданного аргумента.

\begin{lstlisting}
(defun column-matcher (column value)
  (let ((name (name column))
        (predicate (equality-predicate column))
        (normalized (normalize-for-column value column)))
    #'(lambda (row) (funcall predicate (getf row name) normalized))))
\end{lstlisting}

Затем вы создаёте список функций \code{column-matching} для заданных имён и значений,
переданных функции \code{column-matchers}:

\begin{lstlisting}
(defun column-matchers (schema names-and-values)
  (loop for (name value) on names-and-values by #'cddr
     when value collect
       (column-matcher (find-column name schema) value)))
\end{lstlisting}

Теперь вы можете реализовать \code{matching}.  Снова, заметьте, что вы делаете как можно
больше работы за пределами замыкания, чтобы выполнить эти операции один раз при создании
замыкания, а не при его вызове для каждой из строк таблицы.

\begin{lstlisting}
(defun matching (table &rest names-and-values)
  "Build a where function that matches rows with the given column values."
  (let ((matchers (column-matchers (schema table) names-and-values)))
    #'(lambda (row)
        (every #'(lambda (matcher) (funcall matcher row)) matchers))))
\end{lstlisting}

Эта функция выглядит как небольшой клубок замыканий, но стоит пристальней посмотреть на
неё для того, чтобы получить "наслаждение" (FIXME flavor) от возможности программирования
с функциями как объекты первого класса(FIXME?)

Задачей \code{matching} является возврат функции, которая будет выполняться для каждой
строки в таблице для того, чтобы определить -- должна ли эта строка быть включена в
результат, или нет.  Так что функция \code{matching} возвращает замыкание принимающее один
параметр -- строку \code{row}.

Теперь вспомните, что функция \code{EVERY} принимает фунцию-предикат в качестве первого
аргумента, и возвращает истинное значение, только если функция будет возвращать истинное
значение для каждого из элементов списка, который передан \code{EVERY} в качестве второго
аргумента.  Однако, в нашем случае список, переданный \code{EVERY} является списком
функций -- функций отбора для конкретных колонок.  Все что вам нужно знать -- это то, что
каждая функция отбора колонки, при запуске для строки, для которой вы проводите проверку,
возвращает истинное значение.  Так что в качестве функции-предиката для \code{EVERY} вы
передаёте ещё одно замыкание, которое применит \code{FUNCALL} к функции отбора колонки,
передав ей параметр \code{row}.

Другой полезной функцией отбора является \code{in}, которая возвращает функцию, которая
отбирает строки, где значение определённой колонки входит в заданный набор значений.
Функция \code{in} будет принимать два аргумента -- имя колонки, и таблицу, которая
содержит значения, с которыми вы будете сравнивать.  Предположим, например, что вы хотите
найти все песни в базе данных MP3, у которых названия совпадают с названиями песен
исполняемых \textit{Dixie Chicks}.  Вы можете написать это выражение \code{where}
используя функцию \code{in} и вспомогательный запрос, например, вот так:\footnote{Этот
  запрос также вернёт вам все песни исполненяемые \textit{Dixie Chicks}.  Если вы захотите
  ограничить этот запрос, чтобы он содержал только артистов, отличных от \textit{Dixie
    Chicks}, то вам нужна более сложная функция \code{:where}.  Поскольку аргументом
  \code{:where} может быть любая функция, то это можно сделать; вы можете удалить
  собственные песни \textit{Dixie Chicks}' с помощью вот такого запроса:

\begin{lstlisting}
(let* ((dixie-chicks (matching *mp3s* :artist "Dixie Chicks"))
       (same-song (in :song (select :columns :song :from *mp3s* :where dixie-chicks)))
       (query #'(lambda (row) (and (not (funcall dixie-chicks row)) 
                                   (funcall same-song row)))))
  (select :columns '(:artist :song) :from *mp3s* :where query))
\end{lstlisting}

Однако это не особо удобно.  Если вы пишете приложение, которому требуется выполнять много
сложных запросов, то вы можете захотеть придумать более выразительный язык запросов.}

\begin{lstlisting}
(select
  :columns '(:artist :song)
  :from *mp3s*
  :where (in :song 
             (select
               :columns :song
               :from *mp3s*
               :where (matching *mp3s* :artist "Dixie Chicks"))))
\end{lstlisting}

Хотя запросы более сложные, но реализация \code{in} намного проще чем реализация
\code{matching}.

\begin{lstlisting}
(defun in (column-name table)
  (let ((test (equality-predicate (find-column column-name (schema table))))
        (values (map 'list #'(lambda (r) (getf r column-name)) (rows table))))
    #'(lambda (row)
        (member (getf row column-name) values :test test))))
\end{lstlisting}

\section{Работа с результатами выполнения запросов}

Поскольку \code{select} возвращает другую таблицу, вам необходимо немного подумать о том,
как вы будете осуществлять доступ к отдельным значениям.  Если вы уверены, что вы никогда
не измените способ представления данных в таблице, то вы можете просто сделать структуру
таблицы частью API и указать, что класс \code{table} имеет слот \code{rows} который
является вектором содержащим списки свойств, и для доступа к данным в таблице использовать
стандартные функции Common Lisp для работы с векторами и списками свойств.  Но
представление данных -- это внутренняя деталь, которую вы можете захотеть изменить.  Также
вы можете не захотеть, чтобы другие разработчики напрямую работали с данными, например, вы
можете захотеть, чтобы никто не мог с помощью \code{SETF} вставить в строку
ненормализованное значение.  Так что хорошей идеей может быть определение нескольких
абстракций, которые будут обеспечивать нужные вам операции.  Так что, если вы захотите
изменить внутреннее представление данных, то вам нужно будет изменить только реализацию
этих функций и макросов.  И хотя Common Lisp не позволяет вам полностью запретить доступ к
"внутренним" данным, путём предоставления официального API вы по крайней мере сможете
указать где проходит граница, разграничивающая внешнее и внутреннее представление.

Вероятно, наиболее часто используемой операцией с результатами запроса будет итерация по
отдельным строкам, и выделение значений конкретных колонок.  Так что вам нужно
предоставить возможность для выполнения эти операций без прямого доступа к вектору строк
или использования \code{GETF} для получения значения колонки внутри строки.

Реализация этих операция является тривиальной -- эти функции являются лишь врапперами
вокруг кода, который бы вы написали, если у вас не было этих абстракций.  Вы можете
предоставить два способа итерации по строкам таблицы: макрос \code{do-rows}, который
обеспечивает базовый способ организации циклов, и функцию \code{map-rows}, которая создаёт
список, содержащий результаты применения заданной функции к каждой строке
таблицы.\footnote{Версия \code{LOOP} реализованная в M.I.T. до стандартизации Common Lisp
  включала механизм для расширения грамматики \code{LOOP}, которые бы позволяли
  реализовывать итерацию по новым структурам данных.  Некоторые реализации Common Lisp,
  которые унаследовали эту реализацию \code{LOOP}, могут до сих пор иметь эту возможность,
  что делает \code{do-rows} и \code{map-rows} не особо нужными.}

\begin{lstlisting}
(defmacro do-rows ((row table) &body body)
  `(loop for ,row across (rows ,table) do ,@body))

(defun map-rows (fn table)
  (loop for row across (rows table) collect (funcall fn row)))
\end{lstlisting}

Для получения значения конкретной колонки внутри строки, вы должны реализовать функцию
\code{column-value}, которая будет принимать в качестве аргументов строку таблицы и
название колонки, и будет возвращать соответствующее значение.  Опять же, это лишь
тривиальный враппер вокруг кода, который бы вы и так написали.  Но если вы позже измените
внутреннее представление данных, то пользователи \code{column-value} не будут об этом
знать.

\begin{lstlisting}
(defun column-value (row column-name)
  (getf row column-name))
\end{lstlisting}

Хотя \code{column-value} является достаточной абстракцией доступа к значениям колонок, вам
достаточно часто нужно получать одновременный доступ к значениям сразу нескольких колонок.
Так что мы реализуем макрос \code{with-column-values}, который будет связывать набор
переменных со значениями извлечёнными из строки используя соответствующие именованные
параметры.  Так что вместо использования такого кода:

\begin{lstlisting}
 (do-rows (row table)
   (let ((song (column-value row :song))
         (artist (column-value row :artist))
         (album (column-value row :album)))
     (format t "~a by ~a from ~a~%" song artist album)))
\end{lstlisting}

вы можете просто написать следующим образом:

\begin{lstlisting}
(do-rows (row table)
  (with-column-values (song artist album) row
    (format t "~a by ~a from ~a~%" song artist album)))
\end{lstlisting}

И снова, реализация не является очень сложной, если вы используете макрос \code{once-only}
из главы~\ref{ch:08}.

\begin{lstlisting}
(defmacro with-column-values ((&rest vars) row &body body)
  (once-only (row)
    `(let ,(column-bindings vars row) ,@body)))

(defun column-bindings (vars row)
  (loop for v in vars collect `(,v (column-value ,row ,(as-keyword v)))))

(defun as-keyword (symbol)
  (intern (symbol-name symbol) :keyword))
\end{lstlisting}

И в заключение, вы должны предоставить функции для получения количества строк в таблице, а
также для доступа к конкретной строке используя числовой индекс.

\begin{lstlisting}
(defun table-size (table)
  (length (rows table)))

(defun nth-row (n table)
  (aref (rows table) n))
\end{lstlisting}

\section{Другие операции с базой данных}

И в заключение, вы реализуете несколько дополнительных операций с базой данных, которые
будут использованы в главе~\ref{ch:29}.  Первые две из них являются аналогами выражения
\code{DELETE} языка SQL.  Функция \code{delete-rows} используется для удаления из таблицы
строк, соответствующих некоторому критерию.  Также как и \code{select} она принимает
именованные аргументы \code{:from} и \code{:where}.  Но в отличии от \code{select}, эта
функция не возвращает новую таблицу -- вместо этого, она изменяет таблицу, переданную в
качестве аргумента the \code{:from}.

\begin{lstlisting}
(defun delete-rows (&key from where)
  (loop
     with rows = (rows from)
     with store-idx = 0
     for read-idx from 0
     for row across rows
     do (setf (aref rows read-idx) nil)
     unless (funcall where row) do
       (setf (aref rows store-idx) row)
       (incf store-idx)
     finally (setf (fill-pointer rows) store-idx)))
\end{lstlisting}

В интересах повышения производительности, вы можете также реализовать отдельную функцию
для удаления всех строк из таблицы.

\begin{lstlisting}
(defun delete-all-rows (table)
  (setf (rows table) (make-rows *default-table-size*)))
\end{lstlisting}

Оставшиеся функции для работы с базой данных не имеют аналогов среди операций с
реляционными базами данных, но они будут полезны при написании приложения использующего
базу данных MP3.  Первой среди них является функция, которая сортирует строки таблицы
изменяя её.

\begin{lstlisting}
(defun sort-rows (table &rest column-names)
  (setf (rows table) (sort (rows table) (row-comparator column-names (schema table))))
  table)
\end{lstlisting}

С другой стороны, в приложении, работающем с базой данных MP3, вам может понадобиться
функция, которая перемешивает строки в таблице, используя функцию \code{nshuffle-vector}
из главы~\ref{ch:23}.

\begin{lstlisting}
(defun shuffle-table (table)
  (nshuffle-vector (rows table))
  table)
\end{lstlisting}

И в заключение, снова для приложения работающего с базой данных MP3, вы должны реализовать
функцию которая будет выбирать \code{N} произвольных строк, возвращая результат в виде
новой таблицы.  Эта функция также использует \code{nshuffle-vector} вместе с версией
\code{random-sample}, основанной на \textit{Алгоритме S} из книги "Искусство
программирования, т.2.  Получисленные алгоритмы, 3 изд." Дональда Кнута, и который мы
обсуждали в главе~\ref{ch:20}.

\begin{lstlisting}
(defun random-selection (table n)
  (make-instance
   'table
   :schema (schema table)
   :rows (nshuffle-vector (random-sample (rows table) n))))

(defun random-sample (vector n)
  "Based on Algorithm S from Knuth. TAOCP, vol. 2. p. 142"
  (loop with selected = (make-array n :fill-pointer 0)
     for idx from 0
     do
       (loop
          with to-select = (- n (length selected))
          for remaining = (- (length vector) idx)
          while (>= (* remaining (random 1.0)) to-select)
          do (incf idx))
       (vector-push (aref vector idx) selected)
     when (= (length selected) n) return selected))
\end{lstlisting}

Имея данный код, вы будете готовы создать (в главе~\ref{ch:29}) веб-интерфейс для
просмотра коллекции файлов в формате MP3.  Но до этого вам необходимо реализовать часть
сервера, которая будет транслировать поток музыки в формате MP3 используя протокол
Shoutcast, что и является темой следующей главы.

%%% Local Variables: 
%%% mode: latex
%%% TeX-master: "pcl-ru"
%%% TeX-open-quote: "<<"
%%% TeX-close-quote: ">>"
%%% End: 
