\chapter{Специальные операторы}
\label{ch:20}

Между прочим, наиболее впечатляющим аспектом условной системы, описанной в предыдущей
главе, является то, что если бы она не была частью языка, то она могла бы быть полностью
написана в виде отдельной библиотеки.  Это возможно, поскольку специальные операторы
Common Lisp (когда никто не осуществляет прямой доступ к обработке или выдаче условий)
обеспечивают достаточный уровень доступа к низкоуровневым частям языка, что делает
возможным контроль раскрутки стека (unwinding of the stack).

В предыдущих главах я обсуждал наиболее часто используемые специальные операторы, но было
бы неправильным ознакомиться только с их частью.  Для этого есть две причины~--- во-первых,
некоторые из редко используемых специальных операторов используются не часто просто
потому, что то, что они обрабатывают, обычно не используется в большинстве программ.
Ознакомиться с ними будет полезно хотя бы для того, чтобы вы знали, что они существуют.  А
во-вторых, поскольку 25 специальных операторов (вместе с правилами вычисления вызовов
функций и основными типами данных) составляют основу для остальной части языка, то
знакомство с ними поможет вам понять, как язык устроен.

В этой главе я буду обсуждать специальные операторы (некоторые вкратце, а некоторые~---
более подробно), так что вы сможете увидеть как они связаны между собой.  Я буду указывать
на те, которые вы сможете напрямую использовать в вашем коде, на те, которые могут служить
основой для конструкций, которые вы будете использовать все время, а также на те, которые
вы будете редко использовать напрямую, но которые вы сможете использовать в коде,
генерируемом макросами.

\section{Контроль вычисления}

К первой категории специальных операторов относятся три оператора, которые обеспечивают
базовый контроль вычисления выражений. Это \code{QUOTE}, \code{IF} и \code{PROGN}, про
которые я уже рассказывал.  Однако было бы неправильным не отметить то, как каждый из этих
специальных операторов предоставляет контроль за вычислением одной или нескольких форм.
\code{QUOTE} предотвращает вычисление выражения и позволяет вам получить s-выражение в
виде данных. \code{IF} реализует базовый оператор логического выбора, на основе которого
могут быть построены все остальные условные выражения.\footnote{Конечно, если бы \code{IF}
  не был специальным оператором, а некоторым другим условным выражением, таким как
  \code{COND}, то вы могли бы реализовать \code{IF} в виде макроса.  На самом деле, во
  многих диалектах Lisp, начиная с оригинального Lisp McCarthy, \code{COND} был
  примитивным условным оператором.}  А \code{PROGN} обеспечивает возможность вычисления
последовательности выражений.

\section{Манипуляции с лексическим окружением}

Наибольший класс специальных операторов составляют операторы, которые манипулируют и
производят доступ к лексическому окружению. \code{LET} и \code{LET*}, которые мы уже
обсуждали, являются примерами специальных операторов, которые манипулируют лексическим
окружением, поскольку они вводят новые лексические связи для переменных.  Любая
конструкция, такая как \code{DO} или \code{DOTIMES}, которая связывает лексические
переменные, будет развёрнута в \code{LET} или \code{LET*}.\footnote{Конечно, с технической
  точки зрения эти конструкции также будут развёрнуты в \code{LAMBDA}-выражения,
  поскольку, как я упоминял в главе~\ref{ch:06}, \code{LET} может быть определена (и это
делалось в ранних версиях Lisp) в виде макроса, который развёртывается в запуск анонимной
функции.} Специальный оператор \code{SETQ} является одним из операторов для доступа к
лексическому окружению, поскольку он может быть использован для установки значений
переменных, чьи связи были созданы с помощью \code{LET} и \code{LET*}.

Однако, не только переменные могут быть поименованны внутри лексического окружения.  Хотя
большинство функций и определены глобально с использованием \code{DEFUN}, но все равно
возможно создание локальных функций с помощью специальных операторов \code{FLET} и
\code{LABELS}, локальных макросов с помощью \code{MACROLET}, а также специальных видов
макросов (называемых символьными макросами) с помощью \code{SYMBOL-MACROLET}.

Точно также, как и \code{LET} позволяет вам ввести переменную, чьей областью видимости
будет тело \code{LET}, \code{FLET} и \code{LABELS} позволяют вам определить функцию,
которая будет видна только внутри области видимости \code{FLET} или \code{LABELS}.  Эти
специальные операторы являются очень удобными, если вам нужна локальная функция, которая
является слишком сложной для её определения как \code{LAMBDA}, или если вам нужно вызвать
ее несколько раз.  Оба этих оператора имеют одинаковую форму, которая выглядит так:

\begin{lstlisting}
(flet (function-definition*)
  body-form*)
\end{lstlisting}

\noindent{}или так:

\begin{lstlisting}
(labels (function-definition*)
  body-form*)
\end{lstlisting}

\noindent{}где каждая из \code{function-definition} имеет следующую форму:

\begin{lstlisting}
(name (parameter*) form*)
\end{lstlisting}

Разница между \code{FLET} и \code{LABELS} заключается в том, что имена функций, которые
определены с помощью \code{FLET}, могут использоваться только в теле \code{FLET}, в то
время как имена, определённые с помощью \code{LABELS} могут использоваться сразу, включая
тела функций, определённых с помощью \code{LABELS}. Таким образом, \code{LABELS} может
определять рекурсивные функции, а \code{FLET}~--- не может.  Может показаться ограничением
то, что \code{FLET} не может быть использован для определения рекурсивных функций, но
Common Lisp предоставляет и \code{FLET} и \code{LABELS} по той причине, что иногда бывает
полезным иметь возможным написать локальную функцию, которая может вызвать другую функцию
с тем же именем, либо глобальную, либо определённую в охватывающей области видимости.

Внутри тела \code{FLET} или \code{LABELS}, вы можете использовать имена определённых
функций, точно также как и имена любых других функций, включая использование со
специальным оператором \code{FUNCTION}.  Поскольку вы можете использовать \code{FUNCTION}
для получения объекта-функции, представляющего функцию, определённую с помощью \code{FLET}
или \code{LABELS}, и поскольку \code{FLET} и \code{LABELS} могут быть в области видимости
других связывающих форм, таких как \code{LET}, то эти функции могут использоваться как
замыкания (closures).

Поскольку локальные функции могут ссылаться на переменные из охватывающего окружения, то
они могут часто записываться таким образом, чтобы принимать меньше параметров, чем
эквивалентные вспомогательные функции.  Это очень удобно, когда вам необходимо в качестве
параметра-функции передать функцию, которая принимает единственный аргумент. Например, в
следующей функции, которую вы увидите снова в главе~\ref{ch:25}, функция
\code{count-version}, определённая с помощью \code{FLET}, принимает единственный аргумент,
как этого требует функция \code{walk-directory}, но она также может использовать
переменную \code{versions}, заданную охватывающим \code{LET}:

\begin{lstlisting}
(defun count-versions (dir)
  (let ((versions (mapcar #'(lambda (x) (cons x 0)) '(2 3 4))))
    (flet ((count-version (file)
             (incf (cdr (assoc (major-version (read-id3 file)) versions)))))
      (walk-directory dir #'count-version :test #'mp3-p))
    versions))
\end{lstlisting}

Эта функция также может быть записана с использованием анонимной функции вместо
использования \code{FLET}, но задание имени делает исходный текст более понятным.

И когда вспомогательная функция должна быть рекурсивной, она не может быть
анонимной.\footnote{Сюрпризом может показаться то, что в действительности можно сделать
  анонимную функцию рекурсивной.  Однако вы должны будете использовать достаточно
  эзотеричный механизм, известный как <<Y-комбинатор>>.  Но Y-комбинатор является интересным
  теоретическим результатом, а не средством практического программирования, так что мы
  оставим его за пределами данной книги.}  Когда вам не нужно определять рекурсивную
вспомогательную функцию как глобальную функцию, то вы можете использовать \code{LABELS}.
Например, следующая функция, \code{collect-leaves}, использует рекурсивную вспомогательную
функцию \code{walk} для прохода по дереву и сбора всех объектов дерева в список, который
затем возвращается \code{collect-leaves} (после его реверсирования):

\begin{lstlisting}
(defun collect-leaves (tree)
  (let ((leaves ()))
    (labels ((walk (tree)
               (cond
                 ((null tree))
                 ((atom tree) (push tree leaves))
                 (t (walk (car tree))
                    (walk (cdr tree))))))
      (walk tree))
    (nreverse leaves)))
\end{lstlisting}


Снова отметьте, как внутри функции \code{walk} вы можете ссылаться на переменную
\code{leaves}, объявленную окружающим \code{LET}.

\code{FLET} и \code{LABELS} также являются полезными при раскрытии макросов~--- макрос
может раскрываться в код, который содержит \code{FLET} или \code{LABELS} для создания
функций, которые могут быть использованы внутри тела макроса.  Этот приём может быть
использован либо для введения функций, которые будет вызывать пользователь макроса, либо
для организации кода, генерируемого макросом.  Это может служить примером того, как может
быть определена функция, такая как \code{CALL-NEXT-METHOD}, которая может быть
использована только внутри определения метода.

К той же группе, что \code{FLET} и \code{LABELS}, можно отнести специальный оператор
\code{MACROLET}, который вы можете использовать для определения локальных
макросов. Локальные макросы работают также, как и глобальный макросы, определённые с
помощью \code{DEFMACRO}, за тем исключением, что они не затрагивают глобальное
пространство имён.  Когда вычисляется \code{MACROLET}, то выражения в теле вычисляются с
использованием локального определения макроса, которое возможно скрывает глобальное
определение, а также используя локальные определения из окружающих выражений.  Подобно
\code{FLET} и \code{LABELS}, \code{MACROLET} может использоваться напрямую, но оно также
очень удобно для использования кодом, сгенерированным макросом~--- путём обёртки в
\code{MACROLET} некоторого кода, написанного пользователем, макрос может предоставлять
конструкции, которые могут быть использованы только внутри этого кода, или скрывать
глобально определённый макрос.  Вы увидите примеры использования \code{MACROLET} в
главе~\ref{ch:31}.

В заключение, ещё одним специальным оператором для определения макросов является
\code{SYMBOL-MACROLET}, который определяет специальный вид макросов, называемых
символьными макросами (symbol macro).  Символьные макросы аналогичны обычным, за тем
исключением, что они не могут принимать аргументы, и их используют как обычный символ, а
не в листовой записи.  Другими словами, после того, как вы определили символьный макрос с
некоторым именем, любое использование этого символа как значения будет раскрыто, и вместо
него будет вычислена соответствующая форма.  Это как раз относится к тому, как макросы,
такие как \code{WITH-SLOTS} и \code{WITH-ACCESSORS} получают возможность определения
<<переменных>>, которые осуществляют доступ к состоянию определённого объекта.  Например,
следующее выражение \code{WITH-SLOTS}:

\begin{lstlisting}
(with-slots (x y z) foo (list x y z)))
\end{lstlisting}

может быть раскрыто в код, который использует \code{SYMBOL-MACROLET}:

\begin{lstlisting}
(let ((#:g149 foo))
  (symbol-macrolet
      ((x (slot-value #:g149 'x))
       (y (slot-value #:g149 'y))
       (z (slot-value #:g149 'z)))
    (list x y z)))
\end{lstlisting}

Когда вычисляется выражение \code{(list x y z)}, то символы \code{x}, \code{y} и \code{z}
будут раскрыты в соответствующие формы, такие как \lstinline!(slot-value #:g149 'x)!.\footnote{\code{WITH-SLOTS} не обязательно должен быть реализован с помощью
  \code{SYMBOL-MACROLET}~--- в некоторых реализациях, \code{WITH-SLOTS} может проходить по
  коду и раскрывать макрос с \code{x}, \code{y} и \code{z}, уже заменёнными на
  соответствующие формы \code{SLOT-VALUE}.  Вы можете увидеть как это делает ваша
  реализация, с помощью следующего выражения:

\begin{lstlisting}
(macroexpand-1 '(with-slots (x y z) obj (list x y z)))
\end{lstlisting}

Однако, реализации Lisp легче выполнить такую подстановку внутри своего кода, чем кода,
написанного пользователем~--- чтобы заменить \code{x}, \code{y} и \code{z} только в случаях,
когда они используются как значения, проходчик по коду должен понимать синтаксис всех
специальных операторов и уметь рекурсивно раскрывать все макросы, чтобы определить,
включает ли макрос в раскрытой форме эти символы в позиции значения.  Реализации Lisp
имеют соответствующий проходчик по коду, но он относится к той части Lisp, которая
недоступна пользователю языка.}

Символьные макросы наиболее часто используются локально и определяются с помощью
\code{SYMBOL-MACROLET}, но Common Lisp также предоставляет макрос
\code{DEFINE-SYMBOL-MACRO}, который определяет глобальный символьный макрос.  Символьный
макрос, определённый с помощью \code{SYMBOL-MACROLET} скрывает другие макросы с тем же
именем, определённые с помощью \code{DEFINE-SYMBOL-MACRO} или охватывающих выражений
\code{SYMBOL-MACROLET}.

\section{Локальный поток управления}

Следующие четыре специальных оператора, о которых я буду говорить, также создают и
используют имена в лексическом окружении, но для целей изменения контроля потока, а не для
определения новых функций и макросов. Я упоминал ранее все четыре из этих специальных
операторов, потому что они предоставляют низкоуровневые механизмы, используемые для других
особенностей языка. Вот они: \code{BLOCK}, \code{RETURN-FROM}, \code{TAGBODY}, и
\code{GO}. Первые два, \code{BLOCK} и \code{RETURN-FROM}, используются вместе для
написания кода, который совершает выход немедленно из секции кода~--- я говорил про
\code{RETURN-FROM} в главе~\ref{ch:05}, как о способе немедленного выхода из функции, но
он работает и в более общих случаях, чем тот. Два другие, \code{TAGBODY} и \code{GO},
предоставляют вполне низкоуровневую \textit{goto} конструкцию, которая составляет основу для всех
высокоуровневых конструкций цикла, которые вы уже видели.

Общий скелет формы \code{BLOCK} таков:

\begin{lstlisting}
(block name
  form*)
\end{lstlisting}

\textit{name} является символом и \textit{form*}, это формы языка. Формы выполняются по
порядку и значение последней формы возвращается как значение всего \code{BLOCK}, если не
использован \code{RETURN-FROM} для возврата из блока ранее. \code{RETURN-FROM} форма, как
вы видели в главе~\ref{ch:05}, состоит из имени блока, из которого выходят, и, по желанию, формы,
которая предоставляет возвращаемое значение.  Когда \code{RETURN-FROM} выполняется, это
является причиной немедленного выхода из упомянутого в нём \code{BLOCK}.  Если
\code{RETURN-FROM} вызван с формой, возвращающей значение, \code{BLOCK} вернёт это
значение; в ином случае \code{BLOCK} вернёт \code{NIL}.

Именем для \code{BLOCK} может быть любой символ, включая \code{NIL}. Многие из стандартных
макросов конструкций контроля, таких как \code{DO}, \code{DOTIMES} и \code{DOLIST},
генерируют код, состоящий из \code{BLOCK}, названного \code{NIL}. Это позволяет вам
использовать макрос \code{RETURN}, который скорее является синтаксическим сахаром для
\lstinline!(return-from nil ...)!, чтобы прерывать такие циклы. Так, следующий цикл
напечатает не более десяти случайных чисел, остановившись сразу же, как только достигнет
числа большего чем~50:

\begin{lstlisting}
(dotimes (i 10)
  (let ((answer (random 100)))
    (print answer)
    (if (> answer 50) (return))))
\end{lstlisting}

Задающие функции макросы, такие как \code{DEFUN}, \code{FLET} и \code{LABELS}, с другой
стороны, оборачивают свои тела в \code{BLOCK} с тем же именем, что и функция. Вот почему
вы можете пользоваться \code{RETURN-FROM} для возврата из функции.

\code{TAGBODY} и \code{GO} имеют такое же отношение друг к другу, как \code{BLOCK} и
\code{RETURN-FROM}: \code{TAGBODY} задаёт контекст в котором определены имена,
используемые \code{GO}. Скелет \code{TAGBODY} следующий:

\begin{lstlisting}
(tagbody
  tag-or-compound-form*)
\end{lstlisting}

\noindent{}где каждая \textit{tag-or-compound-form}, это или символ, называемый \textit{тег}, или
непустая списочная форма. Форма выполняется по порядку и теги игнорируются, кроме случая,
о котором я скоро скажу. После выполнения последней формы в \code{TAGBODY}, \code{TAGBODY}
возвращает \code{NIL}. В любом месте внутри лексической области видимости \code{TAGBODY}
вы можете использовать специальный оператор \code{GO} для немедленного перехода на любой
тег и выполнение продолжится с формы, следующей за тегом. Например, вы можете написать
простейший бесконечный цикл с \code{TAGBODY} и \code{GO} вроде этого:

\begin{lstlisting}
(tagbody
 top
   (print 'hello)
   (go top))
\end{lstlisting}

Заметьте, что в то время, как имена тегов должны появляться на верхнем уровне в
\code{TAGBODY}, не заключёнными внутри форм, специальный оператор \code{GO} может
появляться где угодно внутри области видимости \code{TAGBODY}. Это означает, что вы можете
написать цикл, который выполняется случайное число раз, например так:

\begin{lstlisting}
(tagbody
 top
   (print 'hello)
   (when (plusp (random 10)) (go top)))
\end{lstlisting}

Ещё более глупый пример \code{TAGBODY}, который показывает вам, что можно иметь множество
тегов в одном \code{TAGBODY}, выглядит так:

\begin{lstlisting}
(tagbody
 a (print 'a) (if (zerop (random 2)) (go c))
 b (print 'b) (if (zerop (random 2)) (go a))
 c (print 'c) (if (zerop (random 2)) (go b)))
\end{lstlisting}

Эта форма будет прыгать вокруг случайно печатаемых \textit{a},\textit{b} и \textit{c} до
тех пор, пока \code{RANDOM} не вернёт~1, и управление наконец достигнет конца
\code{TAGBODY}.

\code{TAGBODY} редко используется прямо, так как почти всегда удобней писать итеративные
конструкции в терминах существующих циклических макросов. Однако он становится удобным для
перевода алгоритмов, написанных на других языках в Common Lisp либо автоматически, либо
вручную. Примером инструмента автоматического перевода является транслятор
FORTRAN-to-Common Lisp, f2cl, который переводит исходный код на Фортране, в исходный код
на Common Lisp для того, чтобы сделать различные библиотеки из Фортрана доступными для
программистов на Common Lisp. Так как многие библиотеки Фортрана были написаны до
революции структурного программирования, они полны операторов \textit{goto}. f2cl
транслятор может просто переводить такие \textit{goto} в \code{GO} внутри соответствующих
\code{TAGBODY}.\footnote{Одна версия f2cl доступна как часть
  \href{http://clocc.sourceforge.net/}{Common Lisp Open Code Collection (CLOCC)}. Для
  контраста, рассмотрим трюки, к которым авторам f2j, FORTRAN-to-Java транслятора,
  пришлось прибегнуть. Хотя Java Virtual Machine (JVM) имеет \textit{goto} инструкцию, она
  не выражена прямо в Java. Таким образом, чтобы скомпилировать все \textit{goto} из
  Фортрана, они сначала компилируют Фортран-код в стандартный java-код с вызовами к FIXME
  классу \textit{dummy} для представления меток и \textit{goto}. Затем они компилируют
  исходник обычным java-компилятором и делают постобработку полученного байт-кода для
  перевода вызовов \textit{dummy} в JVM байт-коды. Умно, но болезненно.}

Аналогично, \code{TAGBODY} и \code{GO} могут быть полезны, когда переводятся алгоритмы,
написанные или прозой, или диаграммами переходов~--- например в классической серии
Дональда Кнута <<Искусство программирования>>, он описывает алгоритмы, используя формат
<<рецептов>>: 

\begin{lstlisting}[style=lisprepl]
step 1, do this; 
step 2, do that; 
step 3, go back to step 2;
\end{lstlisting}

и так далее. Для примера на странице 142, <<Искусства программирования>>, Том 2:
Получисленные алгоритмы, 3-е издание (Addison-Wesley, 1998), он описывает Алгоритм S,
который вы увидите в главе~\ref{ch:27}, в такой форме:

\textit{Алгоритм~S} (метод выбора последовательности). Для выбора n случайных записей из
множества $N$, где $0 < n <= N$.

%TODO: convert to normal list:
\begin{lstlisting}
 * S1. [Инициализировать.] Установить t <-- 0, m <-- 0. (В этом алгоритме m 
представляет количество записей уже выбранных, а t общее количество 
записей которые мы просмотрели.)

 * S2. [Сгенерировать U.] Сгенерировать случайное число U, равномерно 
распределённое между нулём и единицей.

 * S3. [Проверить.] Если (N - t)U >= n - m, то перейти к шагу S5.

 * S4. [Выбрать.] Выбрать следующую запись в последовательность и увеличить
m и t на 1. Если m < n, то перейти к шагу S2; иначе 
последовательность закончена и алгоритм завершается.

 * S5. [Пропустить.] Пропустить следующую запись (не включать её в 
последовательность), увеличить t на 1, и вернуться к шагу S2.
\end{lstlisting}

Это описание может быть легко переведено в  функцию Common Lisp, после переименования
нескольких переменных таким образом:

\begin{lstlisting}
(defun algorithm-s (n max) ; max это N в алгоритме Кнута
  (let (seen               ; t в в алгоритме Кнута
        selected           ; m в алгоритме Кнута
        u                  ; U в алгоритме Кнута
        (records ()))      ; список, где мы сохраняем выбранные записи
    (tagbody
     s1
       (setf seen 0)
       (setf selected 0)
     s2
       (setf u (random 1.0))
     s3
       (when (>= (* (- max seen) u) (- n selected)) (go s5))
     s4
       (push seen records)
       (incf selected)
       (incf seen)
       (if (< selected n)
           (go s2)
           (return-from algorithm-s (nreverse records)))
     s5
       (incf seen)
       (go s2))))
\end{lstlisting}

Это не самый красивый код, но легко проверить, что это канонический перевод алгоритма
Кнута. Однако этот код, в отличии от прозаического описания у Кнута, может быть запущен и
проверен. Затем вы можете начать рефакторинг, проверяя после каждого изменения, что
функция всё ещё работает.\footnote{Так как этот алгоритм зависит от значений, возвращаемых
  \code{RANDOM}, вы, может, захотите проверить его с FIXME одним и тем же произвольным
  зерном (consistent random seed), которое вы можете получить привязывая
  \code{*RANDOM-STATE*} к значению (make-random-state nil) для каждого вызова
  \code{algorithm-s}. Например, вы можете сделать FIXME (basic sanity check) общую
  санитарную проверку для \code{algorithm-s} путём выполнения следующего кода:

\begin{lstlisting}
(let ((*random-state* (make-random-state nil))) (algorithm-s 10 200))
\end{lstlisting}

Если ваша рефакторизация на каждом шаге была правильна, это выражение должно выдавать один
и тот же список каждый раз.}

Сложив все кусочки, вы возможно получите что-то наподобие этого:

\begin{lstlisting}
(defun algorithm-s (n max)
  (loop for seen from 0
     when (< (* (- max seen) (random 1.0)) n)
     collect seen and do (decf n)
     until (zerop n)))
\end{lstlisting}

Хотя это может быть не очевидно, что этот код правильно представляет \textit{Алгоритм~S},
но если вы пришли к нему посредством серии функций, которые вели себя идентично
оригинальному дословному переводу рецепта Кнута, у вас есть хорошее основание верить, что
он правильный.

\section{Раскрутка стека}

Другим интересным аспектом языка является то, что специальные операторы дают вам контроль
за поведением стека вызовов функций. Например, хотя вы обычно используете \code{BLOCK} и
\code{TAGBODY} для управления потоком выполнения команд внутри отдельной функции, вы также
можете использовать их вместе с замыканиями, для выполнения немедленного нелокального
выхода из функции в точку, находящуюся ниже на стеке. Это происходит потому, что имена
\code{BLOCK} и теги \code{TAGBODY} могут быть FIXME closed over by any code внутри
лексического окружения \code{BLOCK} или \code{TAGBODY}. Например, рассмотрим следующую
функцию:

\begin{lstlisting}
(defun foo ()
  (format t "Entering foo~%")
  (block a
    (format t " Entering BLOCK~%")
    (bar #'(lambda () (return-from a)))
    (format t " Leaving BLOCK~%"))
  (format t "Leaving foo~%"))
\end{lstlisting}

Анонимная функция, переданная \code{bar} использует \code{RETURN-FROM} для выхода из
\code{BLOCK}. Но это выражение \code{RETURN-FROM} не будет вычислено до тех пор, пока
анонимная функция не будет выполнена с помощью \code{FUNCALL} или \code{APPLY}. Теперь,
предположим что \code{bar} выглядит следующим образом:

\begin{lstlisting}
(defun bar (fn)
  (format t "  Entering bar~%")
  (baz fn)
  (format t "  Leaving bar~%"))
\end{lstlisting}

Все равно, анонимная функция не будет вызвана. Теперь посмотрим на \code{baz}.

\begin{lstlisting}
(defun baz (fn)
  (format t "   Entering baz~%")
  (funcall fn)
  (format t "   Leaving baz~%"))
\end{lstlisting}

И в заключение, функция выполняется. Но к чему приведёт вызов \code{RETURN-FROM} из блока,
который находится на несколько уровней выше по стеку? На поверку все работает отлично~---
стек отматывается к той точке, где \code{BLOCK} был создан и контроль возвращается из
соответствующего выражения \code{BLOCK}. Вызовы \code{FORMAT} в функциях \code{foo},
\code{bar} и \code{baz} демонстрируют это:

\begin{lstlisting}[style=lisprepl]
  CL-USER> (foo)
  Entering foo
   Entering BLOCK
    Entering bar
     Entering baz
  Leaving foo
  NIL
\end{lstlisting}

Заметьте, что единственным выдаваемым сообщением \code{Leaving . . .} является то, которое
было вставлено после выражения \code{BLOCK} в функции \code{foo}.

Поскольку имена блоков имеют лексическую область видимости, \code{RETURN-FROM} всегда
возвращается из наименьшего охватывающего блока \code{BLOCK} в лексическом окружении, в
котором задана форма \code{RETURN-FROM}, даже если \code{RETURN-FROM} выполняется в
отличающемся динамическом контексте. Например, \code{bar} также может содержать форму
\code{BLOCK} с именем \code{a}, например, вот так:

\begin{lstlisting}
(defun bar (fn)
  (format t "  Entering bar~%")
  (block a (baz fn))
  (format t "  Leaving bar~%"))
\end{lstlisting}

Это дополнительное выражение \code{BLOCK} никак не изменит поведение \code{foo}~--- поиск
имён, таких как \code{a}, производится лексически, во время компиляции, а не динамически,
так что дополнительный блок не влияет на работу \code{RETURN-FROM}. И наборот, имя
\code{BLOCK} может быть использовано только тем выражением \code{RETURN-FROM}, которое
задано внутри лексической области видимости выражения \code{BLOCK}; нет никакой
возможности для кода, который определён вне блока, выполнить выход из него, кроме как
выполнения замыкания, которая выполнит \code{RETURN-FROM} и лексического окружения
\code{BLOCK}.

\code{TAGBODY} и \code{GO} работают точно также как и \code{BLOCK} и \code{RETURN-FROM}.
Когда вы выполняете замыкание, которое содержит выражение \code{GO}, и \code{GO}
вычисляется, то стек отматывается назад к соответствующей форме \code{TAGBODY}, и затем
уже переходит к соответствующему тегу.

Однако, имена блоков \code{BLOCK} и теги \code{TAGBODY}, достаточно сильно отличаются от
связывания лексических переменных. Как обсуждалось в главе~\ref{ch:06}, лексические
связывания имеют неопределённый экстент (FIXME extent), что означает, что связывания не
исчезают даже после того, как связывающая форма будет возвращена. С другой стороны,
выражения \code{BLOCK} и \code{TAGBODY} имеют динамический экстент~--- вы можете выполнить
\code{RETURN-FROM} из \code{BLOCK} или \code{GO} из тега \code{TAGBODY} только когда
\code{BLOCK} или \code{TAGBODY} находятся на стеке вызова функций. Другими словами,
замыкание, которое захватывает имя блока, или тег \code{TAGBODY}, может быть передано вниз
по стеку, для последующего выполнения, но оно не может быть возвращено вверх по
стеку. Если вы выполните замыкание, которое будет пытаться выполнить \code{RETURN-FROM} из
\code{BLOCK}, после того, как \code{BLOCK} будет завершён, то вы получите
ошибку. Аналогично, попытка выполнить \code{GO} для \code{TAGBODY} которое больше не
существует, также вызовет ошибку.\footnote{Это достаточно разумное ограничение~--- не
  совсем понятно, что означает возвращение из формы, которая уже завершилась, если вы
  конечно не программист на Scheme. Scheme поддерживает продолжения (continuations)~---
  языковые конструкции, которые позволяют выполнить возврат из одной и той же функции
  более одного раза. Но по разным причинам, лишь несколько языков, отличных от Scheme
  поддерживают этот вид FIXME continuation.}

Маловероятно, что вам понадобится использовать \code{BLOCK} и \code{TAGBODY} для такого
способа раскрутки стека.  Но вы скорее всего будете использовать этот подход как часть
системы условий и рестартов, так что понимание того, как оно работает, поможет вам понять
что в точности делается при запуске рестарта.\footnote{Если вы относитесь к тем людям,
  которые любят знать как что-то работает, вплоть до мелких деталей, то вы можете
  поразмышлять о том, как вы бы могли реализовать макросы системы условий и рестартов,
  используя \code{BLOCK}, \code{TAGBODY}, замыкания и динамические переменные.}

\code{CATCH} и \code{THROW} являются ещё одной парой специальных операторов, которые
приводят к раскрутке стека.  Вы будете использовать эти операторы ещё реже, чем описанные
выше~--- они являются наследием ранних версий Lisp, которые не имели в своём составе
систему условий Common Lisp.  Они не должны путаться с конструкциями \code{try/catch} и
\code{try/except} из таких языков, как Java и Python.

\code{CATCH} и \code{THROW} являются динамическими аналогами конструкций \code{BLOCK} и
\code{RETURN-FROM}.  Так что вы используете \code{CATCH} для какого-то кода и затем
используете \code{THROW} для выхода из блока \code{CATCH} с возвратом указанного значения.
Разница заключается в том, что связь между \code{CATCH} и \code{THROW} устанавливается
динамически~--- вместо лексически ограниченного имени, метка \code{CATCH} является
объектом, называемым тегом catch, и любое выражение \code{THROW} вычисляется внутри
динамического экстента \code{CATCH}, так что <<выбрасывание>> (throws) этого объекта будет
приводить к раскрутке стека к блоку \code{CATCH} и приводить к немедленному возврату.  Так
что вы можете написать новые версии функций \code{foo}, \code{bar} и \code{baz} используя
\code{CATCH} и \code{THROW} вместо \code{BLOCK} и \code{RETURN-FROM}:

\begin{lstlisting}
(defparameter *obj* (cons nil nil)) ; некоторый произвольный объект

(defun foo ()
  (format t "Entering foo~%")
  (catch *obj*
    (format t " Entering \code{CATCH}~%")
    (bar)
    (format t " Leaving \code{CATCH}~%"))
  (format t "Leaving foo~%"))

(defun bar ()
  (format t "  Entering bar~%")
  (baz)
  (format t "  Leaving bar~%"))

(defun baz ()
  (format t "   Entering baz~%")
  (throw *obj* nil)
  (format t "   Leaving baz~%"))
\end{lstlisting}

Заметьте, что нет необходимости передавать замыкание вниз по стеку~--- \code{baz} может
напрямую вызвать \code{THROW}.  Результат будет таким же как и раньше.

\begin{lstlisting}[style=lisprepl]
  CL-USER> (foo)
  Entering foo
   Entering \code{CATCH}
    Entering bar
     Entering baz
  Leaving foo
  NIL
\end{lstlisting}

Однако, \code{CATCH} и \code{THROW} слишком динамичные.  И в \code{CATCH}, и в
\code{THROW}, форма, представляющая тег, вычисляется, что означает, что её значение в
обоих случаях определяется во время выполнения.  Так что, если некоторый код в \code{bar}
присвоит новое значение \code{*obj*}, то \code{THROW} в \code{baz} не будет пойман в том
же блоке \code{CATCH}.  Это делает использование \code{CATCH} и \code{THROW} более тяжёлым
чем \code{BLOCK} и \code{RETURN-FROM}. Единственным преимуществом версии \code{foo},
\code{bar} и \code{baz}, которая использует \code{CATCH} и \code{THROW}, является то, что
нет необходимости передавать замыкание вниз по стеку, для возврата из \code{CATCH}~---
любой код, который выполняется внутри динамического экстента \code{CATCH} может заставить
вернуться к нему, путём <<бросания>> (FIXME throwing) нужного объекта.

В старых диалектах Lisp в которых не было ничего подобного системе условий Common Lisp,
\code{CATCH} и \code{THROW} использовались для обработки ошибок.  Однако, для того, чтобы
сделать её сопровождаемой, теги catch обычно были FIXME quoted symbols, так что вы могли
понять, глядя на \code{CATCH} и \code{THROW}, где они будут перехвачены во время
выполнения. В Common Lisp вы будете редко иметь нужду в использовании \code{CATCH} и
\code{THROW}, поскольку система условий намного более гибкая.

Последним специальным оператором, относящимся к управлению стеком вызовов, является
\code{UNWIND-PROTECT}, который я вскользь упоминал раньше. \code{UNWIND-PROTECT} позволяет
вам контролировать что происходит при раскрутке стека и быть уверенным в том, что
определённый код всегда выполняется, независимо от того, как поток выполнения покидает
область видимости \code{UNWIND-PROTECT}~--- обычным способом, путём запуска рестарта, или
любым другим способом, описанным в этом разделе.\footnote{\code{UNWIND-PROTECT} является
  эквивалентом конструкции \code{try/finally} в Java и Python.}  Базовая форма
\code{UNWIND-PROTECT} выглядит примерно так:

\begin{lstlisting}
(unwind-protect protected-form
  cleanup-form*)
\end{lstlisting}

Сначала вычисляется одиночное выражение \code{protected-form}, и затем, вне зависимости от
способа его завершения, будут вычислены выражения заданные \code{cleanup-forms}.  Если
\code{protected-form} завершается нормальным образом, то его результат будет возвращён
\code{UNWIND-PROTECT} после вычисления возвратит \code{cleanup-forms}. Выражения
\code{cleanup-forms} вычисляются в том же самом динамическом окружении, что и
\code{UNWIND-PROTECT}, так что все динамические переменные, связи, перезапуски и
обработчики условий, будут доступны коду \code{cleanup-forms}, так как они были видны
перед выполнением \code{UNWIND-PROTECT}.

Вы редко будете использовать \code{UNWIND-PROTECT} напрямую.  Наиболее часто, вы его
будете использовать как основу для макросов в стиле \code{WITH-}, таких как
\code{WITH-OPEN-FILE}, который вычисляет произвольное количество выражений в контексте,
где они имеют доступ к некоторому ресурсу, который должен быть освобождён после того, как
все выполнено, вне зависимости от того, как выражения были завершены~--- нормальным
образом, или через рестарт или любой другой нелокальный выход.  Например, если вы пишете
библиотеку для работы с базами данных, которая определяет функции \code{open-connection} и
\code{close-connection}, то вы можете написать вот такой вот макрос:\footnote{На самом
  деле, CLSQL, интерфейс к множеству баз данных, работающий на многих реализациях Lisp,
  предоставляет макрос с аналогичной функциональностью, имеющий имя \code{with-database}.
  Домашняя страница CLSQL находится по адресу \url{http://clsql.b9.com}.}

\begin{lstlisting}
(defmacro with-database-connection ((var &rest open-args) &body body)
  `(let ((,var (open-connection ,@open-args)))
    (unwind-protect (progn ,@body)
      (close-connection ,var))))
\end{lstlisting}

что позволяет вам писать в следующем стиле:

\begin{lstlisting}
(with-database-connection (conn :host "foo" :user "scott" :password "tiger")
  (do-stuff conn)
  (do-more-stuff conn))
\end{lstlisting}

и не беспокоиться о закрытии соединения к базе данных, поскольку \code{UNWIND-PROTECT}
позаботится о его закрытии, вне зависимости от того, что случится в теле формы
\code{with-database-connection}.

\section{Множественные значения}

Еще одним свойством Common Lisp, которое я упоминал вскользь в главе~\ref{ch:11}, когда я
обсуждал \code{GETHASH}, является возможность возвращения множества значений из одного
выражения. Сейчас мы обсудим эту функциональность более подробно. Правда не совсем
правильно обсуждать эту функциональность в главе про специальные операторы, поскольку этот
функционал не реализуется отдельными операторами, а глубоко интегрирован в
язык. Операторами, которые вы наиболее часто будете использовать с множественными
значениями, являются макросы и функции, а не специальные операторы.  Но базовая
возможность получения множественных значений обеспечивается специальным оператором
\code{MULTIPLE-VALUE-CALL}, на основе которого построен более часто используемый макрос
\code{MULTIPLE-VALUE-BIND}.

Ключом к понимаю множественных значений является тот факт, что возврат множества значений
совершенно отличается от возврата списка~--- если форма возвращает множество значений, то
до тех пор пока вы не сделаете что-то специальное для их получения, все значения, кроме
первого (основного) будут игнорироваться.  Для того, чтобы увидеть это отличие, рассмотрим
функцию \code{GETHASH}, которая возвращает два значения: найденное значение и логическое
значение, которое равно \code{NIL} если значение не было найдено.  Если бы эти значения
возвращались в виде списка, то при каждом вызове \code{GETHASH} вам требовалось бы
выделять найденное значение из списка, вне зависимости от того, нужно ли вам второе
возвращаемое значение или нет.  Предположим, что у вас есть хэш-таблица \code{*h*},
которая содержит числа.  Если бы \code{GETHASH} возвращал бы список, то вы бы не могли
написать что-то подобное:

\begin{lstlisting}
(+ (gethash 'a *h*) (gethash 'b *h*))
\end{lstlisting}

поскольку \code{+} ожидает, что его аргументами будут числа, а не списки. Но поскольку
механизм работы с множественными значениями просто игнорирует дополнительные возвращаемые
значения, которые вам не нужны, то этот код будет работать нормально.

Имеется два аспекта использования множественных значений~--- возврат множественных
значений, и получение не основных значений, возвращаемых формами, которые возвращают
множественные значения.  Начальной точкой для возврата множественных значений являются
функции \code{VALUES} и \code{VALUES-LIST}.  Это обычные функции, а не специальные
операторы, так их параметры передаются как обычно.  \code{VALUES} принимает переменное
число аргументов и возвращает их как множественные значения; \code{VALUES-LIST} принимает
единственный аргумент~--- список значений, и возвращает все его содержимое в виде
множественных значений.  Иначе говоря:

\begin{lstlisting}[style=lisprepl]
  (values-list x) === (apply #'values x)
\end{lstlisting}

Механизм возврата множественных значений зависит от конкретной реализации, также как и
механизм передачи аргументов функциям.  Почти все языковые конструкции возвращающие
значения своих подвыражений, будут <<передавать>> множественные значения, возвращаемые
подвыражениями.  Так что, функция, которая возвращает результат вызова \code{VALUES} или
\code{VALUES-LIST} сама будет возвращать множественные значения, и то же самое будет
делать и функция, вызвавшая эту функцию, и т.д..\footnote{Небольшой набор макросов не
  передаёт дополнительные возвращаемые значения тех выражений, которые они вычисляют.  В
  частности, макрос \code{PROG1}, который вычисляет некоторое количество выражений,
  подобно \code{PROGN}, но возвращает значение первого выражения, возвращает только
  основное значение.  Аналогичным образом, \code{PROG2}, который возвращает значение
  второго выражения, также возвращает лишь основное значение.  Специальный оператор
  \code{MULTIPLE-VALUE-PROG1} является вариантом \code{PROG1}, который возвращает все
  значения первого выражения.  Это небольшой недостаток, что \code{PROG1} не ведёт себя
  также как \code{MULTIPLE-VALUE-PROG1}, но ни один из них не используется достаточно
  часто, чтобы это было неудобным.  Макросы \code{OR} и \code{COND} также не всегда
  прозрачны для множественных значений, возвращая только основное значение определённого
  выражения.}

Но когда выражение вычисляется в позиции значения, то используется только основное
значение~--- вот почему предыдущий пример с добавлением работает как ожидается. Специальный
оператор \code{MULTIPLE-VALUE-CALL} предоставляет механизм, который позволяет вам работать
с множественными значениями, возвращаемыми выражениями. \code{MULTIPLE-VALUE-CALL}
аналогичен \code{FUNCALL} за тем исключением, что \code{FUNCALL} является обычной
функцией, и как следствие~--- использует только основное значение из переданных аргументов,
в то время как \code{MULTIPLE-VALUE-CALL} передаёт функции, указанной в качестве первого
аргумента, все значения, возвращённые остальными выражениями, указанными в качестве
аргументов.

\begin{lstlisting}[style=lisprepl]
  (funcall #'+ (values 1 2) (values 3 4))             ==> 4
  (multiple-value-call #'+ (values 1 2) (values 3 4)) ==> 10
\end{lstlisting}

Но скорее всего вы достаточно редко будете просто передавать все значения, возвращённые
одной функцией, в другую функцию.  Скорее всего вы захотите сохранить множественные
значения в отдельные переменные и затем что-то сделать с ними.  Макрос
\code{MULTIPLE-VALUE-BIND}, с которым вы встречались в главе~\ref{ch:11}, является
наиболее часто используемым оператором для работы с множественными значениями.  В общем
виде он выглядит вот так:

\begin{lstlisting}
(multiple-value-bind (variable*) values-form
  body-form*)
\end{lstlisting}

Выражение \code{values-form} вычисляется, и множественные значения, которые возвращаются
им, присваиваются указанным переменным.  Затем вычисляются выражения \code{body-forms}
используя указанные переменные.  Так что:

\begin{lstlisting}[style=lisprepl]
  (multiple-value-bind (x y) (values 1 2)
    (+ x y)) ==> 3
\end{lstlisting}

Другой макрос~--- \code{MULTIPLE-VALUE-LIST}, ещё более простой~--- он принимает одно
выражение, вычисляет его и собирает полученные множественные значения в список.  Другими
словами, этот макрос выполняет действия обратные действию\code{VALUES-LIST}.

\begin{lstlisting}[style=lisprepl]
  CL-USER> (multiple-value-list (values 1 2))
  (1 2)
  CL-USER> (values-list (multiple-value-list (values 1 2)))
  1
  2
\end{lstlisting}

Однако, если вы обнаружите, что часто используете \code{MULTIPLE-VALUE-LIST}, то это может
быть сигналом того, что некоторая функция должна возвращать список, а не множественные
значения.

И в заключение, если вы хотите присвоить множественные значения, возвращённые формой,
существующим переменным, то вы можете использовать функцию \code{VALUES} для выполнения
\code{SETF}.  Например:

\begin{lstlisting}[style=lisprepl]
  CL-USER> (defparameter *x* nil)
  *X*
  CL-USER> (defparameter *y* nil)
  *Y*
  CL-USER> (setf (values *x* *y*) (floor (/ 57 34)))
  1
  23/34
  CL-USER> *x*
  1
  CL-USER> *y*
  23/34
\end{lstlisting}

\section{EVAL-WHEN}

Еще одним специальным оператором, принципы работы которого вам нужно понимать для того,
чтобы писать некоторые виды макросов, является \code{EVAL-WHEN}.  По некоторым причинам,
книги о Lisp часто считают \code{EVAL-WHEN} орудием только для экспертов.  Но единственным
требованием для понимания \code{EVAL-WHEN} является понимание того, как взаимодействуют
две функции~--- \code{LOAD} и \code{COMPILE-FILE}.  И понимание принципов работы
\code{EVAL-WHEN} будет важным для вас, когда вы начнёте писать сложные макросы, такие как,
мы будем писать в главах~\ref{ch:24} и~\ref{ch:31}.

В предыдущих главах я немного касался вопросов совместной работы \code{LOAD} и
\code{COMPILE-FILE}, но стоит рассмотреть этот вопрос снова.  Задачей \code{LOAD} является
загрузка файла и вычисление всех выражений верхнего уровня, содержащихся в нем.  Задачей
\code{COMPILE-FILE} является компиляция исходного текста в файл FASL, который может быть
затем загружен с помощью \code{LOAD}, так что \code{(load "foo.lisp")} и \code{(load
  "foo.fasl")} являются практически эквивалентными.

Поскольку \code{LOAD} вычисляет каждое выражение до чтения следующего, побочный эффект
вычисления выражений, находящихся ближе к началу файла, может воздействовать на то, как
будут читаться и вычисляться формы, находящиеся дальше в файле.  Например, вычисление
выражения \code{IN-PACKAGE} изменяет значение переменной \code{*PACKAGE*}, что затронет
процедуру чтения всех остальных выражений.\footnote{Причиной того, что загрузка файла с
  выражением \code{IN-PACKAGE} в нем не имеет никакого влияния на значение
  \code{*PACKAGE*} после возврата из \code{LOAD}, является то, что \code{LOAD} связывает
  \code{*PACKAGE*} со своим текущим значением до того, как сделать что-то иное.  Говоря
  другими словами, что-то подобное следующему выражению \code{LET} окружает остальной код
  в реализации \code{LOAD}:

\begin{lstlisting}
(let ((*package* *package*)) ...)
\end{lstlisting}

Любые изменения \code{*PACKAGE*} будут применяться к новой привязке, а старая привязка
будет восстановлена при выходе из \code{LOAD}.  Аналогичным образом, эта функция связывает
переменную \code{*READTABLE*}, которую мы ещё не обсуждали.}  Аналогичным образом,
выражение \code{DEFMACRO} находящееся раньше в файле, может определить макрос, который
будет использоваться кодом, находящимся далее в файле.\footnote{В некоторых реализациях вы
  можете избежать (FIXME get away) вычисления \code{DEFUN}, которые используют
  неопределённые макросы в теле функции, поскольку макросы определяются до того, как
  функция будет вызвана.  Но это будет работать только в тех случаях, когда вы загружаете
  определения с помощью \code{LOAD} из файла с исходным кодом, но не компилируете с
  помощью \code{COMPILE-FILE}, так что в общем, определения макросов должны быть вычислены
  до того, как они будут использованы}

С другой стороны, \code{COMPILE-FILE} обычно не вычисляет выражения в процессе компиляции;
это происходит в момент загрузки файла FASL. Однако \code{COMPILE-FILE} должен вычислять
некоторые выражения, такие как \code{IN-PACKAGE} и \code{DEFMACRO}, чтобы поведение
\code{(load "foo.lisp")} и \code{(load "foo.fasl")} было консистентным.

Так как же работают макросы, такие как \code{IN-PACKAGE} и \code{DEFMACRO}, в тех случаях,
когда они обрабатываются \code{COMPILE-FILE}?  В некоторых версиях Lisp, существовавших до
разработки Common Lisp, компилятор файлов просто знал, что он должен вычислять некоторые
макросы в добавление к их компиляции.  Common Lisp избегает необходимости в таких хаков
путём введения специального оператора \code{EVAL-WHEN} взятого из Maclisp.  Этот оператор,
как и предполагает его имя, позволяет контролировать то, когда определённые части кода
должны вычисляться. В общем виде выражение \code{EVAL-WHEN} выглядит вот так:

\begin{lstlisting}
(eval-when (situation*)
  body-form*)
\end{lstlisting}

Есть три возможных условия (situation)~--- \code{:compile-toplevel}, \code{:load-toplevel}
и \code{:execute}, и то, которое вы укажите, будет определять то, когда будут вычислены
выражения указанные \code{body-forms}.  \code{EVAL-WHEN} с несколькими условиями
аналогичен записи нескольких выражений \code{EVAL-WHEN} с разными условиями, но с
одинаковыми выражениями.  Для объяснения того, что означают эти условия, нам необходимо
объяснить что делает \code{COMPILE-FILE} (который также называют компилятором файлов) в
процессе компиляции файла.

Для того, чтобы объяснить как \code{COMPILE-FILE} компилирует выражения \code{EVAL-WHEN},
я должен описать отличия между компиляцией выражений верхнего уровня (FIXME top-level
form), и компиляцией остальных выражений.  Выражения верхнего уровня, грубо говоря, это
те, которые будут скомпилированы в исполняемый код, который будет выполнен при загрузке
файла FASL.  Так что, все выражения, которые находятся верхнем уровне (FIXME top level of
a source) файла с исходным текстом, будут скомпилированы как выражения верхнего
уровня. Аналогичным образом, все выражения указанные в выражении \code{PROGN} верхнего
уровня, будут также скомпилированы как выражения верхнего уровня (\code{PROGN} сам ничего
не делает~--- он лишь группирует вместе указанные выражения), и они будут выполнены при
загрузке FASL.\footnote{В противоположность этому, выражения, указанные в \code{LET}
  верхнего уровня, не будут скомпилированы как выражения верхнего уровня, потому, что они
  не будут выполняться при загрузке FASL.  Они будут выполнены в контексте привязок,
  созданных \code{LET}. Теоретически, \code{LET} не связывающий никаких переменных может
  рассматриваться как \code{PROGN}, но это не так~--- выражения, указанные в \code{LET}
  никогда не будут считаться выражениями верхнего уровня.}  Аналогичным образом, выражения
указанные в \code{MACROLET} или \code{SYMBOL-MACROLET} будут скомпилированы как выражения
верхнего уровня, поскольку после того, как компилятор раскроет локальные и символьные
макросы, в скомпилированном коде не останется никаких упоминаний \code{MACROLET} или
\code{SYMBOL-MACROLET}.  И в заключение, раскрытие макроса верхнего уровня будет
скомпилировано как выражение верхнего уровня.

Таким образом, \code{DEFUN} указанный на верхнем уровне исходного текста является
выражением верхнего уровня~--- код, который определяет функцию и связывает её с именем,
будет выполнен при загрузке FASL, но выражения внутри тела функции, которые не будут
выполнены до тех пор, пока функция не будет вызвана, не являются выражениями верхнего
уровня.  Большинство выражений компилируются одинаково вне зависимости от того, на верхнем
они уровне они или нет, но семантика выражения \code{EVAL-WHEN} зависит от того, будет ли
оно скомпилировано как выражение верхнего уровня, выражение не верхнего уровня, или просто
вычислено, и все это в комбинации с условиями, указанными в выражении.

Условия \code{:compile-toplevel} и \code{:load-toplevel} контролируют поведение
\code{EVAL-WHEN}, которое компилируется как выражение верхнего уровня.  Когда присутствует
условие \code{:compile-toplevel}, то компилятор файла вычислит заданные выражения во время
компиляции.  Когда указано условие \code{:load-toplevel}, то он будет компилировать
выражения как выражения верхнего уровня.  Если ни одно из этих условий не указано в
выражении \code{EVAL-WHEN} верхнего уровня, то компилятор просто игнорирует его.

Когда \code{EVAL-WHEN} компилируется как выражение не верхнего уровня, то он либо
компилируется как \code{PROGN}, в том случае, если указано условие \code{:execute}, либо
просто игнорируется.  Аналогичным образом, вычисляемое выражение (FIXME evaluated)
\code{EVAL-WHEN} (что включает в себя выражения \code{EVAL-WHEN} верхнего уровня в
исходном тексте, обрабатываемом \code{LOAD} и \code{EVAL-WHEN}, вычисляемый во время
компиляции когда он является подвыражением другого \code{EVAL-WHEN} с условием
\code{:compile-toplevel}) также рассматривается как \code{PROGN} если указано условие
\code{:execute}, и игнорируется в противном случае. (FIXME может быть стоит сделать
табличку с условиями и стадиями компиляции?)

Таким образом, макрос, такой как \code{IN-PACKAGE} может производить необходимые действия
и во время компиляции и при загрузке из исходного кода путём раскрытия в выражения
\code{EVAL-WHEN} выглядящие примерно так:

\begin{lstlisting}
(eval-when (:compile-toplevel :load-toplevel :execute)
  (setf *package* (find-package "PACKAGE-NAME")))
\end{lstlisting}

\noindent{}значение \code{*PACKAGE*} будет выставлено во время компиляции из-за условия
\code{:compile-toplevel}, во время загрузки FASL из-за условия \code{:load-toplevel} и во
время загрузки исходного кода из-за условия \code{:execute}.

Существует два широко распространённых способа использования \code{EVAL-WHEN}. Первый~---
если вы хотите написать макрос, которому необходимо сохранить некоторую информацию во
время компиляции, и которая будет использоваться при генерации раскрытий других
макро-выражений в том же файле.  Это обычно нужно для определяющих (FIXME definitional)
макросов, когда определение, расположенное в начале файла, могут влиять на код,
генерируемый для определения, расположенного далее в том же файле.  Вы будете писать такие
макросы в главе~\ref{ch:24}.

Вам также может понадобиться использовать \code{EVAL-WHEN} если вы захотите поместить
определение макроса и вспомогательной функции, которая используется в этом макросе, в том
же файле с исходным текстом, что и код, использующий данный макрос. \code{DEFMACRO} уже
использует \code{EVAL-WHEN} в своём раскрытии, так что определение макроса становится
доступным для использования сразу.  Но обычно \code{DEFUN} не делает определение функции
доступным во время компиляции, а если вы используете макрос в том же файле, где он
определён, то вам необходимо чтобы были определены и все функции, используемые в
макросе. Если вы обернёте все определения вспомогательных функций в выражение
\code{EVAL-WHEN} с условием \code{:compile-toplevel}, то определения будут доступны при
раскрытии макросов. Вы наверное захотите включить также условия \code{:load-toplevel} и
\code{:execute} поскольку макросы будут требовать наличие определения функций после того,
как файл скомпилирован и загружен, или если вы загружает файл с исходным текстом вместо
компиляции.

\section{Другие специальные операторы}

Все оставшиеся четыре специальных оператора~--- \code{LOCALLY}, \code{THE},
\code{LOAD-TIME-VALUE} и \code{PROGV}, позволяют получить доступ к некоторым частям
нижележащего языка, к которым доступ не может быть осуществлён другими способами.
\code{LOCALLY} и \code{THE} являются частями системы объявлений Common Lisp, которая
используется для <<связывания>> некоторых вещей с компилятором, что не изменит работу
вашего кода, но позволит генерировать лучший код~--- более быстрый, более понятные
сообщения об ошибках, и т.п.\footnote{Одним из объявлений, которая имеет влияние на
  семантику программы, является объявление \code{SPECIAL}, упомянутое в главе~\ref{ch:06}.}
  Мы коротко обсудим объявления в главе~\ref{ch:32}.

Еще два оператора~--- \code{LOAD-TIME-VALUE} и \code{PROGV}, используются не часто, и
объяснение того, почему это происходит, займёт больше времени, чем объяснение того, что
они делают. Я расскажу вам то, что они делают, так что просто вы будете иметь эту
информацию. Когда-нибудь вы встретите один из них, и тогда вы будете готовы к пониманию их
работы.

\code{LOAD-TIME-VALUE} используется (как видно из его имени) для создания значения во
время загрузки.  Когда компилятор обрабатывает код, который содержит выражение
\code{LOAD-TIME-VALUE}, он генерирует код, который выполнит подвыражения лишь один раз, во
время загрузки FASL, и код, содержащий выражение \code{LOAD-TIME-VALUE} будет ссылаться на
вычисленное значение.  Другими словами, вместо того, чтобы писать что-то подобное:

\begin{lstlisting}
(defvar *loaded-at* (get-universal-time))

(defun when-loaded () *loaded-at*)
\end{lstlisting}

\noindent{}вы можете просто написать вот так:

\begin{lstlisting}
(defun when-loaded () (load-time-value (get-universal-time)))
\end{lstlisting}

В коде, не обрабатываемом \code{COMPILE-FILE}, выражение \code{LOAD-TIME-VALUE}
вычисляется однажды, во время компиляции, что может происходить когда вы явно компилируете
функцию с помощью \code{COMPILE}, или даже раньше, из-за неявной компиляции кода во время
его вычисления.  В некомпилируемом коде, \code{LOAD-TIME-VALUE} вычисляет свои
подвыражения при каждом вычислении.

И в заключение, \code{PROGV} создаёт новые динамические привязки для переменных, чьи имена
определяются во время выполнения.  Это в основном полезно при реализации встраиваемых
интерпретаторов для языков, с динамической областью видимости переменных.  Базовая
структура этого выражения выглядит вот так:

\begin{lstlisting}
(progv symbols-list values-list
  body-form*)
\end{lstlisting}

\noindent{}где \code{symbols-list} является выражением, которое вычисляется в список символов, а
\code{values-list}~--- выражение, которое вычисляется в список значений.  Каждый символ
динамически связывается с соответствующим значением, и затем вычисляются выражения,
указанные в \code{body-forms}.  Разница между \code{PROGV} и \code{LET} заключается в том,
что \code{symbols-list} вычисляется во время выполнения, и имена связываемых переменных
вычисляются динамически.  Как я уже сказал, этот оператор не понадобится вам очень часто.

И это вся информация о специальных операторах.  В следующей главе, я вернусь к
практическим темам и покажу вам как использовать пакетную систему Common Lisp для
получения контроля за пространствами имён, так что вы можете писать библиотеки и
приложения, которые смогут сосуществовать без разрушения друг друга.

%%% Local Variables: 
%%% mode: latex
%%% TeX-master: "pcl-ru"
%%% TeX-open-quote: "<<"
%%% TeX-close-quote: ">>"
%%% End: 
