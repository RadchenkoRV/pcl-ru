\chapter{Несколько рецептов для функции FORMAT}
\label{ch:18}

Функция \code{FORMAT} вместе с расширенным макросом \code{LOOP} - одна из двух
возможностей Common Lisp, которые вызывают сильную эмоциональную реакцию у многих
пользователей Common Lisp. Некоторые их любят, другие ненавидят \footnote{Конечно,
  большинство людей осознают, что не стоит возбуждаться по поводу чего-либо в языке
  программирования, и используют или не используют их без сильного беспокойства. С другой
  стороны, интересно, что две эти возможности - единственные в Common Lisp, которые
  реализуют по сути языки предметной области, используя синтаксис, не основанный на
  s-выражениях. Синтаксис управляющих строк \code{FORMAT} основан на символах, в то время
  как расширенный макрос \code{LOOP} может быть понят только в терминах грамматики
  ключевых слов \code{LOOP}. Одна из обычных придирок к \code{FORMAT} и \code{LOOP} - то,
  что они "не достаточно лисповские" - является доказательством того, что программистам на
  Lisp FIXME (Lispers - лисперы?)  действительно нравиться синтаксис, основанный на
  s-выражениях.}

Поклонники \code{FORMAT} любят её за мощь и краткость, в то время как противники её
ненавидят за потенциал для возникновения ошибок и непрозрачность. Сложные управляющие
строки \code{FORMAT} имеют иногда подозрительное сходство с помехами на экране
\footnote{Примечание переводчика: В оригинале - line noise, шум в линии, имеется ввиду
  схожесть строки формата с сигналом на осциллографе \lstinline!~^~\%~---}!, но
\code{FORMAT} остаётся популярным среди программистов на Common Lisp, которые хотят
формировать небольшие куски удобочитаемого текста без необходимости нагромождать кучи
формирующего вывод кода. Хотя управляющие строки \code{FORMAT} могут быть весьма
замысловаты, но во всяком случае единственное \code{FORMAT}-выражение не сильно замусорит
ваш код. Предположим например, что вы хотите напечатать значения в списке, разделенные
запятыми. Вы можете написать так:

\begin{lstlisting}
(loop for cons on list
    do (format t "~a" (car cons))
    when (cdr cons) do (format t ", "))
\end{lstlisting}

Это не очень плохо, но любой, кто будет читать этот код, должен будет мысленно
проанализировать его, прежде чем понять, что все, что он делает - это печать содержимого
списка \code{list} на стандартный вывод. С другой стороны, вы можете с одного взгляда
определить, что следующее выражение печатает список в некоторой форме на стандартный
вывод:

\begin{lstlisting}
(format t "~{~a~^, ~}" list)
\end{lstlisting}

Если вам важна форма, которую примет вывод, тогда вы можете изучить управляющую строку, но
если все, что вы хотите - это приблизительно знать, что делает эта строка кода, то это
можно увидеть сразу.

Во всяком случае, вам следует по меньшей мере зрительно воспринимать \code{FORMAT}, а ещё
лучше разобраться, на что она способна, прежде, чем вы примкнете к про- или
анти-\code{FORMAT}'овскому лагерю. Также важно понимать по меньшей мере основы
\code{FORMAT}, поскольку другие стандартные функции, такие, как функции выбрасывания
условий, обсуждаемые в следующей главе, используют управляющие строки в стиле
\code{FORMAT} для формирования вывода.

Чтобы сильнее запутать дело, \code{FORMAT} поддерживает три совершенно разных вида
форматирования: печать таблиц с данными, структурная печать (\textit{pretty printing})
s-выражений, и формирование удобочитаемых сообщений со вставленными FIXME (interpolated?
вложенными?) значениями. Печать таблиц с текстовыми данными на сегодня несколько устарела;
это одно из напоминаний, что Lisp стар, как \code{FORTRAN}. В действительности, некоторые
директивы, которые вы можете использовать для печати значений с плавающей точкой внутри
полей с фиксированной длинной были основаны прямо на edit descriptors FIXME (дескрипторах
редактирования?) FORTRAN, которые использовались в FORTRAN для чтения и печати столбцов с
данными, расположенными внутри полей с фиксированной длинной. Тем не менее, использование
Common Lisp как замены FORTRAN выходят за рамки этой книги, так что я не буду обсуждать
эти аспекты \code{FORMAT}.

Структурная печать также находится за рамками этой книги - не потому, что она устарела, а
потому, что это слишком большая тема. Вкратце, механизм структурной печати Common Lisp -
это настраиваемая система для печати блочно-структурированных данных, включая, но не
ограничиваясь, s-выражениями, применяющаяся, когда необходимы переменные отступы и
динамически увеличивающиеся переводы строк. Это отличный инструмент при необходимости, но
не часто требуемый в повседневном программировании. \footnote{Читатели, интересующиеся в
  механизом структурной печати, могут почитать статью "XP: A Common Lisp Pretty Printing
  System" Ричарда Уотерса (Richard Waters). Это описание системы структурированной печати,
  которая в итоге была включена в Common Lisp. Вы можете найти загрузить её с
  \url{ftp://publications.ai.mit.edu/ai-publications/pdf/AIM-1102a.pdf}.}

Вместо этого я сосредоточусь на частях \code{FORMAT}, которые вы можете использовать,
чтобы формировать удобочитаемые строки со вставленными в них значениями. Даже ограничивая
обзор таким образом, остаётся изрядное количество материала. Не чувствуйте себя так, как
будто вы обязаны помнить каждую деталь, описанную в этой главе. Вы можете достичь многого
с лишь несколькими идиомами \code{FORMAT}. Сначала я опишу наиболее важные возможности
\code{FORMAT}; а вам решать, насколько вы хотите стать волшебником \code{FORMAT}.
 
\section{Функция FORMAT}

Как вы видели в предыдущих главах, функция \code{FORMAT} принимает два аргумента:
получатель для своего вывода, и управляющую строку, которая содержит буквенный текст и
вложенные \textit{директивы}. Любые дополнительные аргументы предоставляют значения,
используемые директивами управляющей строки, которые вставляют эти значения в печатаемый
текст. Я буду ссылаться на эти аргументы как на \textit{аргументы формата}.

Первым аргументом \code{FORMAT}, получателем для печатаемого текста, может быть \code{T},
\code{NIL}, поток, или строка с указателем заполнения. \code{Т} обозначает поток
\code{*STANDARD-OUTPUT*}, в то время как \code{NIL} заставляет \code{FORMAT} сформировать
свой вывод в виде строки, которую функция затем возвращает \footnote{Чтобы немного
  запутать дело, большинство остальных функций ввода/вывода также принимают \code{T} и
  \code{NIL} как указатели потоков, но с небольшим отличием: как указатель потока,
  \code{Т} обозначает двунаправленный поток \code{*TERMINAL-IO*}, тогда как \code{NIL}
  указывает на \code{*STANDARD-OUTPUT*} как на стандартный поток вывода и
  \code{*STANDARD-INPUT*} как на стандартный поток ввода.} Если получатель - поток, то
вывод пишется в поток. А если получатель - строка с указателем заполнения, то
форматированный вывод добавляется к концу строки и указатель заполнения соответственно
выравнивается. За исключением случая, когда получатель - \code{NIL} и функция возвращает
строку, \code{FORMAT} возвращает \code{NIL}.

Второй аргумент - управляющая строка, является, в сущности, программой на языке
\code{FORMAT}. Язык \code{FORMAT} не целиком "лисповый" - его основной синтаксис основан
на символах, а не на s-выражениях, и оптимизирован для краткости, а не для легкого
понимания. Вот почему сложные управляющие строки \code{FORMAT} могут быть приняты за
помехи.

Большинство из директив \code{FORMAT} просто вставляют аргумент внутрь выводимого текста в
той или иной форме. Некоторые директивы, такие как \lstinline!~\%!, которая заставляет
\code{FORMAT} выполнить перевод строки, не используют никаких аргументов. Другие, как вы
увидите, могут использовать более одного аргумента. Одна из директив даже позволяет вам
прыгать по списку аргументов, с целью обработки одного и того же аргумента несколько раз,
или в некоторых ситуациях пропустить определённые аргументы. Но прежде, чем я буду
обсуждать конкретные директивы, давайте взглянем на общий синтаксис директив.

\section{Директивы FORMAT}

Все директивы начинаются с тильды (\lstinline!~!) и кончаются отдельным знаком, который
идентифицирует директиву. Вы можете писать этот символ как в верхнем, так и в нижнем
регистре. Некоторые директивы принимают \textit{префиксные параметры}, которые пишутся
непосредственно после тильды, разделяются запятыми, и используются для управления такими
деталями, как - сколько разрядов печатать после десятичной точки при печати числа с
плавающей точкой. Например, директива \lstinline!~\$!, одна из директив, использующихся
для печати значений c плавающей точкой, по умолчанию печатает два разряда, следующие за
десятичной точкой.

\begin{verbatim}
  CL-USER> (format t "~$" pi)
  3.14
  NIL
\end{verbatim}

Тем не менее с префиксным параметром вы можете указать чтобы функция печатала свой
аргумент, к примеру, с пятью десятичными знаками, как здесь:

\begin{verbatim}
  CL-USER> (format t "~5$" pi)
  3.14159
  NIL
\end{verbatim}

Значениями префиксного параметра являются либо числа, записанные как десятичные, или
знаки, записанные в виде одинарной кавычки, за которой следует нужный символ. Значение
префиксного параметра может быть также получено из аргумента формата двумя способами:
префиксный параметр \code{v} заставляет \code{FORMAT} использовать один аргумент формата и
назначить его значение префиксному параметру. Префиксный параметр \lstinline!#! будет вычислен
как количество оставшихся аргументов формата. Например:

\begin{verbatim}
  CL-USER> (format t "~v$" 3 pi)
  3.142
  NIL
  CL-USER> (format t "~#$" pi)
  3.1
  NIL
\end{verbatim}

Я дам более правдоподобные примеры использования аргумента \lstinline!#! в разделе "Условное форматирование".

Вы можете также опустить оба префиксных параметра. Впрочем, если вы хотите указать один
параметр, но не желаете указывать параметры, стоящие перед ним, то вы должны включить
запятую для каждого пропущенного параметра. Например, директива \lstinline!~F!, другая
директива для печати значений с плавающей точкой, также принимает параметр для управления
количеством десятичных разрядов при печати, но это второй по счету параметр, а не
первый. Если вы хотите использовать \lstinline!~F! для печати числа с пятью десятичными
разрядами, вы можете написать так:

\begin{verbatim}
  CL-USER> (format t "~,5f" pi)
  3.14159
  NIL
\end{verbatim}

Также вы можете изменить поведение некоторых директив при помощи \textit{модификаторов}
\lstinline!:! и знака \lstinline!@!, которые ставятся после любого префиксного параметра и
до идентифицирующего директиву знака. Эти модификаторы незначительно меняют поведение
директивы. Например, с модификатором \lstinline!:!, директива \lstinline!~D!,
использующаяся для вывода целых чисел в десятичном виде, создаёт число с запятыми,
разделяющими каждые три разряда, в то время как знак \lstinline!@! заставляет
\lstinline!~D!  включить знак плюс в случае положительного числа.

\begin{verbatim}
  CL-USER> (format t "~d" 1000000)
  1000000
  NIL
  CL-USER> (format t "~:d" 1000000)
  1,000,000
  NIL
  CL-USER> (format t "~@d" 1000000)
  +1000000
  NIL
\end{verbatim}

В случае необходимости вы можете объединить модификаторы \lstinline!:! и \lstinline!@!,
для того чтобы получить оба варианта:

\begin{verbatim}
  CL-USER> (format t "~:@d" 1000000)
  +1,000,000
  NIL
\end{verbatim}

В директивах, где оба модифицированных варианта поведения не могут быть осмысленно
объединены, использование обоих модификаторов либо не определено или приобретает третье
значение.

\section{Основы Форматирования}

Сейчас вы готовы познакомится с конкретными директивами. Я начну с нескольких наиболее
распространенных директив, включая несколько тех, которые вы уже встречали в предыдущих
главах.

Наиболее универсальная директива - \lstinline!~A!, которая использует один аргумент
формата любого типа и печатает его в \textit{эстетичной} (удобочитаемой) форме. Например,
строки печатаются без кавычек и экранирующих символов (escape characters), а числа
печатаются в форме, принятой для соответствующего числового типа. Если вы хотите просто
получить значение, предназначенное для прочтения человеком, то эта директива - ваш выбор.

\begin{verbatim}
  (format nil "The value is: ~a" 10)           ==> "The value is: 10"
  (format nil "The value is: ~a" "foo")        ==> "The value is: foo"
  (format nil "The value is: ~a" (list 1 2 3)) ==> "The value is: (1 2 3)"
\end{verbatim}

Родственная директива, \lstinline!~S!, также требует один аргумент формата любого типа, и
печатает его. Однако \lstinline!~S!  пытается сформировать такой вывод, который мог бы
быть прочитан обратно с помощью \code{READ}. Поэтому, строки должны быть заключены в
кавычки, при необходимости знаки должны быть пакетно-специлизированы FIXME
(package-qualified?), и так далее. Объекты, которые не имеют подходящего для \code{READ}
представления печатаются в нечитаемом синтаксисе объектов FIXME, \lstinline!#<>!. С
модификатором \textit{двоеточие}, обе директивы \lstinline!~A! и \lstinline!~S!  порождают
\code{NIL} в виде \code{()}, а не как \code{NIL}. Обе директивы \lstinline!~A! и
\lstinline!~S! также принимают до четырёх префиксных параметров, которые могут быть
использованы для выравнивания пробелами FIXME (padding?), добавляемыми после (или до, при
модификаторе \textit{@}) значения, впрочем эти функции действительно полезны лишь при
формировании табличных данных.

Две другие часто используемые директивы - это \lstinline!~\%!, которая создаёт новую
строку, и \lstinline!~&!, которая выполняет \textit{перевод строки}. Разница между ними в
том, что \lstinline!~\%! всегда создаёт новую строку, тогда как \lstinline!~&!
срабатывает только если она уже не находится в начале строки. Это удобно при создании
слабо связанных функций, каждая из которых формирует кусок текста, и их нужно объединить
различными способами. Например, если одна из функции выводит текст, который кончается
новой строкой (\lstinline!~\%!), а другая функция выводит некоторый текст, который
начинается с перевода строки (\lstinline!~&!), то вам не стоит беспокоиться о получении
дополнительной пустой строки, если вы вызываете их одну за другой. Обе эти директивы могут
принимать единственный префиксный параметр, который обозначает количество выводимых новых
строк. Директива \lstinline!~\%! просто выведет заданное количество знаков новой строки, в
то время как директива \lstinline!~&! создаст либо \code{n - 1} либо \code{n} новых строк,
в зависимости от того, начинает ли она с начала строки.

Реже используется родственная им директива \lstinline!~~!, которая заставляет
\code{FORMAT} вывести знак тильды. Подобно директивам \lstinline!~\%! и \lstinline!~&!,
она может быть параметризована числом, которое задает количество выводимых тильд.

\section{Знаковые и целочисленные директивы}

Вдобавок к директивам общего назначения, \lstinline!~A! и \lstinline!~S!, \code{FORMAT}
поддерживает несколько директив, которые могут использоваться для получения значений
определённых типов особыми способами. Простейшая из них является директива \lstinline!~C!,
которая используется для вывода знаков. Ей не требуются префиксные аргументы, но её работу
можно корректировать с помощью модификаторов \textit{двоеточие} и \textit{@}. Без
модификаций её поведение на отличается от поведения \lstinline!~A!, за исключением того,
что она работает только со знаками. Модифицированные версии более полезны. С модификатором
\textit{двоеточие}, \lstinline!~:C! выводит заданные по имени \textit{непечатаемые} знаки,
такие как пробел, символ табуляции и перевод строки. Это полезно, если вы хотите создать
сообщение к пользователю, в котором упоминается некоторый символ. Например, следующий код:

\begin{lstlisting}
(format t "Syntax error. Unexpected character: ~:c" char)
\end{lstlisting}

может напечатать такое сообщения:

\begin{verbatim}
  Syntax error. Unexpected character: a
\end{verbatim}

а ещё вот такое:

\begin{verbatim}
  Syntax error. Unexpected character: Space
\end{verbatim}

Вместе с модификатором \lstinline!@!, \lstinline!~@С! выведет знак в синтаксисе знаков
Lisp:

\begin{verbatim}
  CL-USER> (format t "~@c~%" #\a)
  #\a
  NIL
\end{verbatim}

Одновременно с модификаторами \lstinline!:! и \textit{@}, директива \lstinline!~C! может
напечатать дополнительную информацию о том, как ввести символ с клавиатуры, если для этого
требуется специальная клавиатурная комбинация. Например, на Macintosh, в некоторых
приложениях вы можете ввести нулевой символ (код символа 0 в ASCII или в любом
надмножестве ASCII, наподобие ISO-8859-1 или Unicode) удерживая клавиши Control и нажав
'\lstinline!@!'. В OpenMCL, если вы напечатаете нулевой символ c помощью директивы
\lstinline!~:C!, то она сообщит вам следующее:
  
\begin{verbatim}
  (format nil "~:@c" (code-char 0)) ==> "^@ (Control @)"
\end{verbatim}

Однако не все версии Lisp реализуют этот аспект директивы \lstinline!~C!. Даже если они
это делают, то реализация может и не быть аккуратной - например, если вы запустите OpenMCL
в SLIME, комбинация клавиш \lstinline!C-@! перехватывается Emacs, вызывая команду
\code{set-mark-command}\footnote{Этот вариант директивы \lstinline!~C! имеет большее
  значение на платформах наподобие Lisp-машин, где нажатие клавиши представляется знаками
  Lisp.}
	
Другой важной категорией являются директивы формата, предназначенные для вывода
чисел. Хотя для этого вы можете использовать директивы \lstinline!~A! и \lstinline!~S!, но
если вы хотите получить над печатью чисел больший контроль, вам следует использовать одну
из специально предназначенных для чисел директив. Ориентированные на числа директивы могут
быть подразделены на две категории: директивы для форматирования целых чисел и директивы
для форматирования чисел с плавающей точкой.

Вот пять родственных директив для форматированного вывода целых чисел: \lstinline!~D!,
\lstinline!~X!, \lstinline!~O!, \lstinline!~B!, и \lstinline!~R!. Чаще всего применяется
директива \lstinline!~D!, которая выводит целые числа по основанию \code{10}.

\begin{verbatim}
  (format nil "~d" 1000000) ==> "1000000"
\end{verbatim}

Как я упоминал ранее, с модификатором \lstinline!:! она добавляет запятые.

\begin{verbatim}
  (format nil "~:d" 1000000) ==> "1,000,000"
\end{verbatim}

А с модификатором \lstinline!@!, она всегда будет печатать знак.

\begin{verbatim}
  (format nil "~@d" 1000000) ==> "+1000000"
\end{verbatim}

И оба модификатора могут быть объединены.

\begin{verbatim}
  (format nil "~:@d" 1000000) ==> "+1,000,000"
\end{verbatim}

С помощью первого префиксного параметра можно задать минимальный размер вывода, а с
помощью второго~--- используемый символ заполнения. По умолчанию символ заполнения - это
пробел, и заполнение всегда помещается до самого числа.

\begin{verbatim}
  (format nil "~12d" 1000000)    ==> "     1000000"
  (format nil "~12,'0d" 1000000) ==> "000001000000"
\end{verbatim}

Эти параметры удобны для форматирования объектов наподобие календарных дат в формате с
фиксированной длинной.

\begin{verbatim}
  (format nil "~4,'0d-~2,'0d-~2,'0d" 2005 6 10) ==> "2005-06-10"
\end{verbatim}

Третий и четвертый параметры используются в связке с модификатором \textit{двоеточие}:
третий параметр определяет знак, используемый в качестве разделителя между группами и
разрядами, а четвертый параметр определяет число разрядов в группе. Их значения по
умолчанию - запятая и число 3 соответственно. Таким образом, вы можете использовать
директиву \lstinline!~:D! без параметров для вывода больших чисел в стандартном для
Соединенных Штатов формате, но можете заменить запятую на точку и группировку с \code{3}
на \code{4} с помощью \lstinline!~,,'.,4D!.

\begin{verbatim}
  (format nil "~:d" 100000000)       ==> "100,000,000"
  (format nil "~,,'.,4:d" 100000000) ==> "1.0000.0000"
\end{verbatim}

Заметим, что вы должны использовать запятые для замещения позиций неуказанных параметров
длины и заполняющего символа, позволяя им сохранить свои значения по умолчанию.

Директивы \lstinline!~X!, \lstinline!~O!, и \lstinline!~B! работают подобно директиве
\lstinline!~D!, за исключением того, что они выводят числа в шестнадцатеричном,
восьмеричном, и двоичном виде.

\begin{verbatim}
  (format nil "~x" 1000000) ==> "f4240"
  (format nil "~o" 1000000) ==> "3641100"
  (format nil "~b" 1000000) ==> "11110100001001000000"
\end{verbatim}

Наконец, директива ~R - универсальная директива для задания \textit{системы счисления}. Её
первый параметр - число между 2 и 36 (включительно), которое обозначает, какое основание
системы счисления использовать. Оставшиеся параметры такие же, что и четыре параметра,
принимаемые директивами \lstinline!~D!, \lstinline!~X!, \lstinline!~O!, и \lstinline!~B!,
а модификаторы \textit{двоеточие} и \lstinline!@! меняют её поведение схожим
образом. Кроме того, директива \lstinline!~R! ведет себя особым образом при использовании
без префиксных параметров, который я буду обсуждать в разделе "Директивы для Английского
языка".

\section{Директивы для Чисел с Плавающей Точкой}

Вот четыре директивы для форматирования значений с плавающей точкой: \lstinline!~F!,
\lstinline!~E!, \lstinline!~G! и \lstinline!~\$!. Первые три из них - это директивы,
основанные на дескрипторах редактирования FIXME (edit descriptor?) FORTRAN. Я пропущу
большинство деталей этих директив, поскольку они в основном имеют дело с форматированием
чисел с плавающей точкой для использования в табличной форме. Тем не менее вы можете
использовать директивы \lstinline!~F!, \lstinline!~E!, и \lstinline!~\$! для вставки
значений с плавающей точкой в текст.  С другой стороны директива \lstinline!~G!, или
\textit{генерал}, FIXME (генерал?)  сочетает аспекты директив \lstinline!~F! и
\lstinline!~E! единственным осмысленным способом для печати таблиц.

Директива \lstinline!~F! печатает свой аргумент, который должен быть числом
\footnote{Технически, если аргумент не является вещественным числом, предполагается, что
  \lstinline!~F! форматирует его так же, как это сделала бы директива ~D, которая ведет
  себя как директива \lstinline!~A!, если аргумент не является числом, но не все
  реализации ведут себя должным образом}, в десятичном формате, по возможности контролируя
количество разрядов после десятичной точки. Директиве \lstinline!~F!, тем не менее,
разрешается использовать компьютеризированную научную нотацию FIXME (компьютеризированное
экспоненциальное представление?), если число достаточно велико либо мало. Директива
\lstinline!~E!, с другой стороны, всегда выводит числа в компьютеризированной научной
нотации. Обе эти директивы принимают несколько префиксных параметров, но вам нужно
беспокоиться только о втором, который управляет количеством разрядов, печатаемых после
десятичной точки.

\begin{verbatim}
  (format nil "~f" pi)   ==> "3.141592653589793d0"
  (format nil "~,4f" pi) ==> "3.1416"
  (format nil "~e" pi)   ==> "3.141592653589793d+0"
  (format nil "~,4e" pi) ==> "3.1416d+0"
\end{verbatim}

Директива \lstinline!~\$! (значок доллара) похожа на \lstinline!~F!, но несколько
проще. Как подсказывает её имя, она предназначена для вывода денежных единиц. Без
параметров она эквивалентна \lstinline!~,2F!. Чтобы изменить количество разрядов,
печатаемых после десятичной точки, используйте \textit{первый} параметр, в то время как
второй параметр регулирует минимальное количество разрядов, печатающихся до десятичной
дочки.

\begin{verbatim}
  (format nil "~$" pi) ==> "3.14"
  (format nil "~2,4$" pi) ==> "0003.14"
\end{verbatim}

С модификатором \lstinline!@! все три директивы, \lstinline!~F!, \lstinline!~E! и
\lstinline!~\$! можно заставить всегда печатать знак, плюс или минус \footnote{Итак, вот
  что говорит стандарт языка. По какой-то причине, возможно коренящейся в общей
  унаследованной кодовой базе, некоторые реализации Common Lisp реализуют этот аспект
  директивы \lstinline!~F! некорректно}.

\section{Директивы для английского языка}

Некоторые из удобнейших директив \code{FORMAT} для формирования удобочитаемых сообщений -
те, которые выводят английский текст. Эти директивы позволяют вам выводить числа как
английский текст, выводить слова в множественном числе в зависимости от значения аргумента
формата, и менять регистр FIXME (case conversion?  выполнять преобразование между
строчными и прописными буквами?) в секциях вывода \code{FORMAT}.

Директива \lstinline!~R!, которую я обсуждал в разделе "Знаковые и Целочисленные
директивы", при использовании без указания системы счисления, печатает числа как
английские слова или римские цифры. При использовании без префиксного параметра и
модификоторов, она выводит число словами как количественное числительное.

\begin{verbatim}
  (format nil "~r" 1234) ==> "one thousand two hundred thirty-four"
\end{verbatim}

С модификатором \textit{двоеточие} она выводит число как порядковое числительное.

\begin{verbatim}
  (format nil "~:r" 1234) ==> "one thousand two hundred thirty-fourth"
\end{verbatim}

И вместе с модификатором \lstinline!@!, она выводит число в виде римских цифр; вместе с
\lstinline!@! и \textit{двоеточием} она выводит римские цифры, в которых четверки и
девятки записаны как \code{IIII} и \code{VIIII} вместо \code{IV} и \code{IX}.

\begin{verbatim}
  (format nil "~@r" 1234)  ==> "MCCXXXIV"
  (format nil "~:@r" 1234) ==> "MCCXXXIIII"
\end{verbatim}

Для чисел, слишком больших, чтобы быть представленными в заданной форме, \lstinline!~R!
ведет себя как \lstinline!~D!.

Чтобы помочь вам формировать сообщений со словами в нужном числе, \code{FORMAT}
предоставляет директиву \lstinline!~P!, которая просто выводит 's' если соответствующий
аргумент не \code{1}.

\begin{verbatim}
  (format nil "file~p" 1)  ==> "file"
  (format nil "file~p" 10) ==> "files"
  (format nil "file~p" 0)  ==> "files"
\end{verbatim}

Тем не меннее обычно вы будете использовать \lstinline!~P! вместе с модификатором
\textit{двоеточие}, который заставляет её повторно обработать предыдущий аргумент формата.

\begin{verbatim}
  (format nil "~r file~:p" 1)  ==> "one file"
  (format nil "~r file~:p" 10) ==> "ten files"
  (format nil "~r file~:p" 0)  ==> "zero files"
\end{verbatim}

С модификатором \lstinline!@!, который может быть объединен с модификатором
\textit{двоеточие}, \lstinline!~P! выводит \textit{y} или \textit{ies}.

\begin{verbatim}
  (format nil "~r famil~:@p" 1)  ==> "one family"
  (format nil "~r famil~:@p" 10) ==> "ten families"
  (format nil "~r famil~:@p" 0)  ==> "zero families"
\end{verbatim}

Очевидно, что \lstinline!~P! не может разрешить все проблемы образования множественного
числа и не может помочь при формировании сообщений на других языках (отличных от
английского), она удобна в тех ситуациях, для которых предназначена. А директива
\lstinline!~[!, о которой я расскажу очень скоро, предоставит вам более гибкий способ
параметризации вывода \code{FORMAT}.

Последняя директива, посвященная выводу английского текста - это \lstinline!~(!, которая
позволяет вам контролировать регистр выводимого текста. Каждая \lstinline!~(! составляет
пару с \lstinline!~)!, и весь вывод, сгенерированный частью управляющей строки между двумя
маркерами будет преобразован в нижний регистр.

\begin{verbatim}
  (format nil "~(~a~)" "FOO") ==> "foo"
  (format nil "~(~@r~)" 124)  ==> "cxxiv
\end{verbatim}

Вы можете изменить поведение \lstinline!~(! модификатором \lstinline!@!, чтобы заставить
ее начать с прописной буквы первое слово на участке текста, с \textit{двоеточием}
заставить её печатать все слова с прописной буквы, а с обоими модификаторами -
преобразовать весь текст в верхний регистр. (Подходящие для этой директивы \textit{слова}
представляют собой буквенно-цифровые знаки, ограниченные не-буквенно-цифровыми знаками или
концом текста.)

\begin{verbatim}
  (format nil "~(~a~)" "tHe Quick BROWN foX")   ==> "the quick brown fox"
  (format nil "~@(~a~)" "tHe Quick BROWN foX")  ==> "The quick brown fox"
  (format nil "~:(~a~)"p "tHe Quick BROWN foX")  ==> "The Quick Brown Fox"
  (format nil "~:@(~a~)" "tHe Quick BROWN foX") ==> "THE QUICK BROWN FOX"
\end{verbatim}

\section{Условное Форматирование}

Вдобавок к директивам, вставляющим в выводимый текст свои аргументы и видоизменяющими
прочий вывод, \code{FORMAT} предоставляет несколько директив, который реализуют простые
управляющие структуры внутри управляющих строк. Одна из них, которую вы использовали в
главе~\ref{ch:09}, это \textit{условная} директива \lstinline!~[!. Эта директива
замыкается соответствующей директивой \lstinline!~]!, а между ними находятся выражения,
разделенные \lstinline!~;!. Работа директивы \lstinline!~[!  - выбрать одно из выражений,
которое затем обрабатывается в \code{FORMAT}. Без модификаторов или параметров, выражение
выбирается по числовому индексу; директива \lstinline!~[! использует аргумент формата,
который должен быть числом, и выбирает \textit{N}-ное (считая от нуля) выражение, где
\textit{N} - значение аргумента.

\begin{verbatim}
  (format nil "~[cero~;uno~;dos~]" 0) ==> "cero"
  (format nil "~[cero~;uno~;dos~]" 1) ==> "uno"
  (format nil "~[cero~;uno~;dos~]" 2) ==> "dos"
\end{verbatim}

Если значение аргумента больше ,чем число выражений, то ничего не печатается.

\begin{verbatim}
  (format nil "~[cero~;uno~;dos~]" 3) ==> ""
\end{verbatim}

Однако если последний разделитель выражений это \lstinline!~:;! вместо \lstinline!~;,!
тогда последнее выражение служит выражением по умолчанию.

\begin{verbatim}
  (format nil "~[cero~;uno~;dos~:;mucho~]" 3)   ==> "mucho"
  (format nil "~[cero~;uno~;dos~:;mucho~]" 100) ==> "mucho"
\end{verbatim}

Также возможно определить выбранное выражение, используя префиксный параметр. Хотя было бы
глупо использовать константу, записанную прямо в управляющей строке, вспомним, что знак
'\lstinline!#!', используемый в качестве префиксного параметра означает число оставшихся
для обработки аргументов. Поэтому вы можете определить строку формата следующим образом:

\begin{lstlisting}
(defparameter *list-etc*
  "~#[NONE~;~a~;~a and ~a~:;~a, ~a~]~#[~; and ~a~:;, ~a, etc~].")
\end{lstlisting}

и использовать её так:

\begin{verbatim}
  (format nil *list-etc*)                ==> "NONE."
  (format nil *list-etc* 'a)             ==> "A."
  (format nil *list-etc* 'a 'b)          ==> "A and B."
  (format nil *list-etc* 'a 'b 'c)       ==> "A, B and C."
  (format nil *list-etc* 'a 'b 'c 'd)    ==> "A, B, C, etc."
  (format nil *list-etc* 'a 'b 'c 'd 'e) ==> "A, B, C, etc."
\end{verbatim}

Заметим, что управляющая строка в действительности содержит две \lstinline!~[~]! директивы
- обе из которых применяют \lstinline!#! для выбора используемого выражения. Первая
директива использует от нуля до двух аргументов, тогда как вторая использует ещё один,
если он есть. \code{FORMAT} молча проигнорирует любые аргументы, сверх использованных во
время обработки управляющей строки.

С модификатором \textit{двоеточие} \lstinline!~[! может содержать только два выражения;
директива использует единственный аргумент и обрабатывает первое выражение, если аргумент
\code{NIL}, и второе выражение в противном случае. Вы уже использовали этот вариант
\lstinline!~[! в главе 9 для формирования сообщений типа принять/отклонить FIXME
(pass/fail message? сработало или не сработало?), таких как это:

\begin{lstlisting}
(format t "~:[FAIL~;pass~]" test-result)
\end{lstlisting}

Заметим, что оба выражения могут быть пустыми, но директива должна содержать
\lstinline!~;!.

Наконец, с модификатором \lstinline!@!, директива \lstinline!~[! может иметь только одно
выражение. Директива использует первый аргумент и если он отличен от \code{NIL},
обрабатывает выражение, при этом список аргументов восстанавливается заново, чтобы первый
аргумент был доступен для использования заново.

\begin{verbatim}
  (format nil "~@[x = ~a ~]~@[y = ~a~]" 10 20)   ==> "x = 10 y = 20"
  (format nil "~@[x = ~a ~]~@[y = ~a~]" 10 nil)  ==> "x = 10 "
  (format nil "~@[x = ~a ~]~@[y = ~a~]" nil 20)  ==> "y = 20"
  (format nil "~@[x = ~a ~]~@[y = ~a~]" nil nil) ==> ""
\end{verbatim}

\section{Итерация}

Другая директива \code{FORMAT}, мимоходом виденная вами, это директива итерации
\lstinline!~{!. Эта директива сообщает \code{FORMAT} перебрать элементы списка или
  неявного списка аргументов формата.

Без модификаторов, \lstinline!~{! принимает один аргумент формата, который должен являться списком. Подобно директиве
\lstinline!~[!, которой всегда соответствует директива \lstinline!~]!, директива \lstinline!~{! всегда имеет соответствующую замыкающую
\lstinline!~!}. Текст между двумя маркерами обрабатывается как управляющая строка, которая выбирает свой аргумент из
списка, поглощенного FIXME (consumed?)  директивой \lstinline!~{!. \code{FORMAT} будет циклически обрабатывать эту
управляющую строку до тех пор, пока в перебираемом списке не останется элементов. В следующем примере, \lstinline!~{!
принимает один аргумент формата, список \code{(1 2 3)}, и затем обрабатывает управляющую строку "\lstinline!~a, !", повторяя,
пока все элементы списка не будут использованы.

\begin{verbatim}
  (format nil "~{~a, ~}" (list 1 2 3)) ==> "1, 2, 3, "
\end{verbatim}

При этом раздражает, что при печати за последним элементом списка следует запятая и пробел. Вы можете исправить
это директивой \lstinline!~^!; внутри тела \lstinline!~{! директива \lstinline!~^! заставляет итерацию немедленно остановиться, и если в
списке не остаётся больше элементов, прервать обработку оставшейся части управляющей строки. Таким образом, для
предотвращения печати запятой и пробела после последнего элемента в списке, вы можете предварить его \lstinline!~^!.

\begin{verbatim}
  (format nil "~{~a~^, ~}" (list 1 2 3)) ==> "1, 2, 3"
\end{verbatim}

После первых двух итераций, при обработке \lstinline!~^!, в списке остаются необработанные
элементы. При этом на третий раз, после того как директива \lstinline!~a! обработает
\code{3}, \lstinline!~^! заставит \code{FORMAT} прервать итерацию без печати запятой и
пробела.

С модификатором \lstinline!@!, \lstinline!~{! обработает оставшийся аргумент формата как список.

\begin{verbatim}
  (format nil "~@{~a~^, ~}" 1 2 3) ==> "1, 2, 3"
\end{verbatim}

Внутри тела \lstinline!~{...~}! специальный префиксный параметр \lstinline!#! ссылается на число необработанных элементов списка, а
не на число оставшихся элементов формата. Вы можете использовать его вместе с директивой \lstinline!~[! для печати
разделенного запятыми списка с "and" перед последним элементом, вот так:

\begin{verbatim}
  (format nil "~{~a~#[~;, and ~:;, ~]~}" (list 1 2 3)) ==> "1, 2, and 3"
\end{verbatim}

Тем не менее этот подход совершенно не работает, когда список имеет длину в два элемента,
поскольку тогда добавляется лишняя запятая.

\begin{verbatim}
  (format nil "~{~a~#[~;, and ~:;, ~]~}" (list 1 2)) ==> "1, and 2"
\end{verbatim}

Вы можете поправить это кучей способов. Следующий пользуется эффектом, который даёт директива \lstinline!~@{!, когда она
заключена внутри другой директивы \lstinline!~{! или \lstinline!~@{! - в этом случае она перебирает все элементы, оставшиеся в
обрабатываемом внешней директивой \lstinline!~{! списке. Вы можете объединить её с директивой \lstinline!~#[! чтобы следующая
управляющая строка форматировала список в соответствии с английской грамматикой:

\begin{verbatim}
  (defparameter *english-list*
    "~{~#[~;~a~;~a and ~a~:;~@{~a~#[~;, and ~:;, ~]~}~]~}")

  (format nil *english-list* '())        ==> ""
  (format nil *english-list* '(1))       ==> "1"
  (format nil *english-list* '(1 2))     ==> "1 and 2"
  (format nil *english-list* '(1 2 3))   ==> "1, 2, and 3"
  (format nil *english-list* '(1 2 3 4)) ==> "1, 2, 3, and 4"
\end{verbatim}

В то время, как эта управляющая строка приближается к такому классу кода, который трудно понять, после того как
он написан FIXME (write-only code?), все-таки это возможно, когда у вас есть немного времени. Внешние
\lstinline!~{...~}! принимают список и затем перебирают его. Затем все тело цикла состоит из \lstinline!~#[...~];! печать
производится на каждом шаге цикла, и таким образом зависит от количества обрабатываемых элементов, оставшихся в
списке. Разделяя директиву \lstinline!~#[...~]! разделителями выражений \lstinline!~;! вы можете увидеть, что она состоит из
четырёх выражений, последнее из которых является выражением по умолчанию, поскольку его предваряет \lstinline!~:;! в
отличие от простой \lstinline!~;!. Первое выражение, выполняющееся при нулевом числе элементов, пусто, оно нужно только
в том случае, если обрабатываемых элементов больше не осталось, тогда мы должны остановиться. Второе выражение с
помощью простой директивы \lstinline!~a! обрабатывает случай, когда элемент единственный. С двумя элементами справляется
"\lstinline!~a and ~a!". И выражение по умолчанию, которое имеет дело с тремя и более элементами, состоит из другой
директивы итерации, в этот раз используется \lstinline!~@{! для перебора оставшихся элементов списка, обрабатываемых внешней
\lstinline!~{!. В итоге тело цикла представляет собой управляющую строку, которая может корректно обработать список трёх
или более элементов, что в данной ситуации более чем достаточно. Поскольку цикл \lstinline!~@{! использует все
оставшиеся элементы списка, внешний цикл выполняется только один раз.

Если вы хотите напечатать что-нибудь особенное, например "\lstinline!<empty>!", когда
список пуст, то у вас есть пара способов это сделать. Возможно проще всего будет вставить
нужный вам текст в первое (нулевое) выражение внешней \lstinline!~#[! и затем добавить
модификатор \textit{двоеточие} к замыкающей \lstinline!~}! внешнего цикла - двоеточие
заставит цикл выполниться по меньшей мере один раз, даже если список пуст, и в этот момент
\code{FORMAT} обработает нулевое выражение условной директивы.

\begin{verbatim}
  (defparameter *english-list*
    "~{~#[<empty>~;~a~;~a and ~a~:;~@{~a~#[~;, and ~:;, ~]~}~]~:}")

  (format nil *english-list* '()) ==> "<empty>"
\end{verbatim}

Удивительно, что директива \lstinline!~{! предоставляет даже больше вариантов с различными комбинациями префиксных
параметров и модификаторов. Я не буду обсуждать их, только скажу, что вы можете использовать целочисленный
префиксный параметр для ограничения максимального числа выполнений итерации, и что с модификатором \textit{двоеточие}
каждый элемент списка (как настоящего, так и созданного директивой \lstinline!~@{!) сам должен быть списком, чьи
элементы затем будут использованы как аргументы управляющей строки в директиве \lstinline!~:{...~}!.

\section{Тройной прыжок}

Более простой директивой является \lstinline!~*!, которая позволяет вам перемещаться по
списку аргументов формата. В своей основной форме, без модификаторов, она просто
пропускает следующий аргумент, при этом ничего не печатается. Однако чаще она используется
с модификатором \textit{двоеточие}, который заставляет её отступить назад, позволяя одному
и тому же аргументу быть обработанным дважды. Например, вы можете использовать
\lstinline!~:*! для печати числового аргумента один раз словом, а другой раз цифрами, вот
так:

\begin{verbatim}
  (format nil "~r ~:*(~d)" 1) ==> "one (1)"
\end{verbatim}

Еще вы можете реализовать директиву, похожую на \lstinline!~:P! для неправильной формы множественного числа (в английском
языке) объединяя \lstinline!~:*! с \lstinline!~[!.

\begin{verbatim}
  (format nil "I saw ~r el~:*~[ves~;f~:;ves~]." 0) ==> "I saw zero elves."
  (format nil "I saw ~r el~:*~[ves~;f~:;ves~]." 1) ==> "I saw one elf."
  (format nil "I saw ~r el~:*~[ves~;f~:;ves~]." 2) ==> "I saw two elves."
\end{verbatim}

В этой управляющей строке \lstinline!~R! печатает аргумент формата в виде количественного числительного. Затем директива
\lstinline!~:*! возвращается назад, так что число также используется как аргумент директивы \lstinline!~[!, выбирая между
выражениями, когда число равно нулю, единице, или чему-нибудь ещё \footnote{Если вы находите фразу "I saw zero elves"
("Я видел нуль эльфов") немного неуклюжей, то можете использовать слегка усовершенствованную управляющую строку,
которая использует ~:* иначе, вот таким образом:

\begin{verbatim}
  (format nil "I saw ~[no~:;~:*~r~] el~:*~[ves~;f~:;ves~]." 0) ==> "I saw no elves."
  (format nil "I saw ~[no~:;~:*~r~] el~:*~[ves~;f~:;ves~]." 1) ==> "I saw one elf."
  (format nil "I saw ~[no~:;~:*~r~] el~:*~[ves~;f~:;ves~]." 2) ==> "I saw two elves."
\end{verbatim}

}

Внутри директивы \lstinline!~{!, \lstinline!~*! пропускает или перескакивает назад через элементы списка. Например, вы можете
напечатать только ключи в списке свойств FIXME (использовать термин "список свойств" или plist?), таким образом:

\begin{verbatim}
  (format nil "~{~s~*~^ ~}" '(:a 10 :b 20)) ==> ":A :B"
\end{verbatim}

Директиве \lstinline!~*! также может быть задан префиксный параметр. Без модификаторов или
с модификатором \textit{двоеточие}, этот параметр определяет число аргументов, на которое
нужно передвинуться вперед или назад, и по умолчанию равняется единице. С модификатором
\lstinline!@!, префиксный параметр определяет абсолютный, отсчитываемый от нуля индекс
аргумента, на который нужно перемеситься, по умолчанию это нуль. Вариант \lstinline!~*! с
\lstinline!@! может быть полезен, если вы хотите использовать различные управляющие строки
для формирования различных сообщений для одних и тех же аргументов, и если этим сообщениям
нужно использовать свои аргументы в различном порядке \footnote{Эта проблема может
  возникнуть при попытке локализовать приложение и перевести удобочитаемые сообщения на
  различные языки. \code{FORMAT} может помочь с некоторыми из этих проблем, но это далеко
  на полноценная система локализации.}

\section{И многое другое...}

Осталось ещё многое - я не упоминал о директиве \lstinline!~?!, которая может брать
фрагменты управляющих строк из аргументов формата, или директиву \lstinline!~/!, которая
позволяет вызвать произвольную функцию для обработки следующего аргумента формата. И ещё
остались все директивы для формирования табличной и удобочитаемой печати. Но уже
затронутых в этой главе директив пока будет вполне достаточно.

В следующей главе вы перейдете к системе условий FIXME Common Lisp, аналогичной системам
исключений и обработки ошибок, присутствующих в других языках.


%%% Local Variables: 
%%% mode: latex
%%% TeX-master: "pcl-ru"
%%% TeX-open-quote: "<<"
%%% TeX-close-quote: ">>"
%%% End: 

